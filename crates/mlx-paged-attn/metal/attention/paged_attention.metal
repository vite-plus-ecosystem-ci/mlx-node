// Updated from MLX commit has f70764a

#include "../utils.metal"
#include "../float8.metal"
#include <metal_simdgroup>
#include <metal_stdlib>

using namespace metal;

// ========================================== Generic vector types

// A vector type to store Q, K, V elements.
template <typename T, int VEC_SIZE> struct Vec {};

// A vector type to store FP32 accumulators.
template <typename T> struct FloatVec {};

// Template vector operations.
template <typename Acc, typename A, typename B> inline Acc mul(A a, B b);

template <typename T> inline float sum(T v);

template <typename T> inline float dot(T a, T b) {
  return sum(mul<T, T, T>(a, b));
}

template <typename A, typename T> inline float dot(T a, T b) {
  return sum(mul<A, T, T>(a, b));
}

// FP32 vector data types.
struct Float8_ {
  float4 x;
  float4 y;
};

template <> struct Vec<float, 1> {
  using Type = float;
};
template <> struct Vec<float, 2> {
  using Type = float2;
};
template <> struct Vec<float, 4> {
  using Type = float4;
};
template <> struct Vec<float, 8> {
  using Type = Float8_;
};

template <> struct FloatVec<float> {
  using Type = float;
};
template <> struct FloatVec<float2> {
  using Type = float2;
};
template <> struct FloatVec<float4> {
  using Type = float4;
};
template <> struct FloatVec<Float8_> {
  using Type = Float8_;
};

template <> inline float mul(float a, float b) { return a * b; }

template <> inline float2 mul(float2 a, float2 b) { return a * b; }

template <> inline float4 mul(float4 a, float4 b) { return a * b; }

template <> inline Float8_ mul(Float8_ a, Float8_ b) {
  Float8_ c;
  c.x = a.x * b.x;
  c.y = a.y * b.y;
  return c;
}

template <> inline float sum(float a) { return a; }

template <> inline float sum(float2 a) { return a.x + a.y; }

template <> inline float sum(float4 a) { return a.x + a.y + a.z + a.w; }

template <> inline float sum(Float8_ a) { return sum(a.x) + sum(a.y); }

inline Float8_ fma(Float8_ a, Float8_ b, Float8_ c) {
  Float8_ res;
  res.x = fma(a.x, b.x, c.x);
  res.y = fma(a.y, b.y, c.y);
  return res;
}

inline void from_float(thread float &dst, float src) { dst = src; }
inline void from_float(thread float2 &dst, float2 src) { dst = src; }
inline void from_float(thread float4 &dst, float4 src) { dst = src; }
inline void from_float(thread Float8_ &dst, Float8_ src) { dst = src; }

// BF16 vector data types.
// #if defined(__HAVE_BFLOAT__)

// struct Bfloat8_ {
//   bfloat4 x;
//   bfloat4 y;
// };

// template<>
// struct Vec<bfloat, 1> {
//   using Type = bfloat;
// };
// template<>
// struct Vec<bfloat, 2> {
//   using Type = bfloat2;
// };
// template<>
// struct Vec<bfloat, 4> {
//   using Type = bfloat4;
// };
// template<>
// struct Vec<bfloat, 8> {
//   using Type = Bfloat8_;
// };

// template<>
// struct FloatVec<bfloat> {
//   using Type = float;
// };
// template<>
// struct FloatVec<bfloat2> {
//   using Type = float2;
// };
// template<>
// struct FloatVec<bfloat4> {
//   using Type = float4;
// };
// template<>
// struct FloatVec<Bfloat8_> {
//   using Type = Float8_;
// };

// template<>
// inline float mul(bfloat a, bfloat b) {
//   return (float)a * (float)b;
// }
// template<>
// inline bfloat mul(bfloat a, bfloat b) {
//   return a*b;
// }

// template<>
// inline float2 mul(bfloat2 a, bfloat2 b) {
//   return (float2)a * (float2)b;
// }
// template<>
// inline bfloat2 mul(bfloat2 a, bfloat2 b) {
//   return a * b;
// }

// template<>
// inline float4 mul(bfloat4 a, bfloat4 b) {
//   return (float4)a * (float4)b;
// }
// template<>
// inline bfloat4 mul(bfloat4 a, bfloat4 b) {
//   return a * b;
// }

// template<>
// inline Float8_ mul(Bfloat8_ a, Bfloat8_ b) {
//   Float8_ c;
//   c.x = mul<float4, bfloat4, bfloat4>(a.x, b.x);
//   c.y = mul<float4, bfloat4, bfloat4>(a.y, b.y);
//   return c;
// }
// template<>
// inline Bfloat8_ mul(Bfloat8_ a, Bfloat8_ b) {
//   Bfloat8_ c;
//   c.x = mul<bfloat4, bfloat4, bfloat4>(a.x, b.x);
//   c.y = mul<bfloat4, bfloat4, bfloat4>(a.y, b.y);
//   return c;
// }

// template<>
// inline float sum(bfloat a) {
//   return (float)a;
// }

// template<>
// inline float sum(bfloat2 a) {
//   return (float)a.x + (float)a.y;
// }

// template<>
// inline float sum(bfloat4 a) {
//   return sum(a.x) + sum(a.y);
// }

// template<>
// inline float sum(Bfloat8_ a) {
//   return sum(a.x) + sum(a.y);
// }

// inline float fma(bfloat a, bfloat b, float c) {
//   return (float)a * (float)b + c;
// }

// inline float2 fma(bfloat2 a, bfloat2 b, float2 c) {
//   return (float2)a * (float2)b + c;
// }

// inline float4 fma(bfloat4 a, bfloat4 b, float4 c) {
//   return (float4)a * (float4)b + c;
// }

// inline Float8_ fma(Bfloat8_ a, Bfloat8_ b, Float8_ c) {
//   Float8_ res;
//   res.x = fma((float4)a.x, (float4)b.x, (float4)c.x);
//   res.y = fma((float4)a.y, (float4)b.y, (float4)c.y);
//   return res;
// }
// inline Bfloat8_ fma(Bfloat8_ a, Bfloat8_ b, Bfloat8_ c) {
//   Bfloat8_ res;
//   res.x = (bfloat4)fma((float4)a.x, (float4)b.x, (float4)c.x);
//   res.y = (bfloat4)fma((float4)a.y, (float4)b.x, (float4)c.y);
//   return c;
// }

// inline void from_float(thread bfloat& dst, float src) {
//   dst = static_cast<bfloat>(src);
// }
// inline void from_float(thread bfloat2& dst, float2 src) {
//   dst.x = static_cast<bfloat>(src.x);
//   dst.y = static_cast<bfloat>(src.y);
// }
// inline void from_float(thread bfloat4& dst, float4 src) {
//   dst.x = static_cast<bfloat>(src.x);
//   dst.y = static_cast<bfloat>(src.y);
//   dst.z = static_cast<bfloat>(src.z);
//   dst.w = static_cast<bfloat>(src.w);
// }
// inline void from_float(thread Bfloat8_& dst, Float8_ src) {
//   bfloat4 x;
//   bfloat4 y;
//   from_float(x, src.x);
//   from_float(y, src.y);
//   dst.x = x;
//   dst.y = y;
// }

// #else

struct Bfloat2_ {
  bfloat16_t x;
  bfloat16_t y;
};

struct Bfloat4_ {
  Bfloat2_ x;
  Bfloat2_ y;
};

struct Bfloat8_ {
  Bfloat4_ x;
  Bfloat4_ y;
};

template <> struct Vec<bfloat16_t, 1> {
  using Type = bfloat16_t;
};
template <> struct Vec<bfloat16_t, 2> {
  using Type = Bfloat2_;
};
template <> struct Vec<bfloat16_t, 4> {
  using Type = Bfloat4_;
};
template <> struct Vec<bfloat16_t, 8> {
  using Type = Bfloat8_;
};

template <> struct FloatVec<bfloat16_t> {
  using Type = float;
};
template <> struct FloatVec<Bfloat2_> {
  using Type = float2;
};
template <> struct FloatVec<Bfloat4_> {
  using Type = float4;
};
template <> struct FloatVec<Bfloat8_> {
  using Type = Float8_;
};

template <> inline float mul(bfloat16_t a, bfloat16_t b) {
  return (float)a * (float)b;
}
template <> inline bfloat16_t mul(bfloat16_t a, bfloat16_t b) { return a * b; }

template <> inline float2 mul(Bfloat2_ a, Bfloat2_ b) {
  float2 a_f((float)a.x, (float)a.y);
  float2 b_f((float)b.x, (float)b.y);
  return a_f * b_f;
}
template <> inline Bfloat2_ mul(Bfloat2_ a, Bfloat2_ b) {
  Bfloat2_ c;
  c.x = a.x * b.x;
  c.y = a.y * b.y;
  return c;
}

template <> inline float4 mul(Bfloat4_ a, Bfloat4_ b) {
  float2 x = mul<float2, Bfloat2_, Bfloat2_>(a.x, b.x);
  float2 y = mul<float2, Bfloat2_, Bfloat2_>(a.y, b.y);
  float4 c;
  c.x = x.x;
  c.y = x.y;
  c.z = y.x;
  c.w = y.y;
  return c;
}
template <> inline Bfloat4_ mul(Bfloat4_ a, Bfloat4_ b) {
  Bfloat4_ c;
  c.x = mul<Bfloat2_, Bfloat2_, Bfloat2_>(a.x, b.x);
  c.y = mul<Bfloat2_, Bfloat2_, Bfloat2_>(a.y, b.y);
  return c;
}

template <> inline Float8_ mul(Bfloat8_ a, Bfloat8_ b) {
  Float8_ c;
  c.x = mul<float4, Bfloat4_, Bfloat4_>(a.x, b.x);
  c.y = mul<float4, Bfloat4_, Bfloat4_>(a.y, b.y);
  return c;
}
template <> inline Bfloat8_ mul(Bfloat8_ a, Bfloat8_ b) {
  Bfloat8_ c;
  c.x = mul<Bfloat4_, Bfloat4_, Bfloat4_>(a.x, b.x);
  c.y = mul<Bfloat4_, Bfloat4_, Bfloat4_>(a.y, b.y);
  return c;
}

template <> inline float sum(bfloat16_t a) { return (float)a; }

template <> inline float sum(Bfloat2_ a) { return (float)a.x + (float)a.y; }

template <> inline float sum(Bfloat4_ a) { return sum(a.x) + sum(a.y); }

template <> inline float sum(Bfloat8_ a) { return sum(a.x) + sum(a.y); }

inline float fma(bfloat16_t a, bfloat16_t b, float c) {
  return (float)a * (float)b + c;
}
inline bfloat16_t fma(bfloat16_t a, bfloat16_t b, bfloat16_t c) {
  return a * b + c;
}

inline float2 fma(Bfloat2_ a, Bfloat2_ b, float2 c) {
  float2 a_f((float)a.x, (float)a.y);
  float2 b_f((float)b.x, (float)b.y);
  return a_f * b_f + c;
}
inline Bfloat2_ fma(Bfloat2_ a, Bfloat2_ b, Bfloat2_ c) {
  Bfloat2_ res;
  res.x = a.x * b.x + c.x;
  res.y = a.y * b.y + c.y;
  return res;
}

inline float4 fma(Bfloat4_ a, Bfloat4_ b, float4 c) {
  float4 res;
  res.x = fma(a.x.x, b.x.x, c.x);
  res.y = fma(a.x.y, b.x.y, c.y);
  res.z = fma(a.y.x, b.y.x, c.z);
  res.w = fma(a.y.y, b.y.y, c.w);
  return res;
}
inline Bfloat4_ fma(Bfloat4_ a, Bfloat4_ b, Bfloat4_ c) {
  Bfloat4_ res;
  res.x = fma(a.x, b.x, c.x);
  res.y = fma(a.y, b.y, c.y);
  return res;
}

inline Float8_ fma(Bfloat8_ a, Bfloat8_ b, Float8_ c) {
  float4 x = fma(a.x, b.x, c.x);
  float4 y = fma(a.y, b.y, c.y);
  Float8_ res;
  res.x = x;
  res.y = y;
  return res;
}
inline Bfloat8_ fma(Bfloat8_ a, Bfloat8_ b, Bfloat8_ c) {
  Bfloat8_ res;
  res.x = fma(a.x, b.x, c.x);
  res.y = fma(a.y, b.y, c.y);
  return res;
}

inline void from_float(thread bfloat16_t &dst, float src) {
  dst = static_cast<bfloat16_t>(src);
}
inline void from_float(thread Bfloat2_ &dst, float2 src) {
  dst.x = static_cast<bfloat16_t>(src.x);
  dst.y = static_cast<bfloat16_t>(src.y);
}
inline void from_float(thread Bfloat4_ &dst, float4 src) {
  dst.x.x = static_cast<bfloat16_t>(src.x);
  dst.x.y = static_cast<bfloat16_t>(src.y);
  dst.y.x = static_cast<bfloat16_t>(src.z);
  dst.y.y = static_cast<bfloat16_t>(src.w);
}
inline void from_float(thread Bfloat8_ &dst, Float8_ src) {
  Bfloat4_ x;
  Bfloat4_ y;
  from_float(x, src.x);
  from_float(y, src.y);
  dst.x = x;
  dst.y = y;
}

// #endif

// FP16 vector data types.
struct Half8_ {
  half4 x;
  half4 y;
};

template <> struct Vec<half, 1> {
  using Type = half;
};
template <> struct Vec<half, 2> {
  using Type = half2;
};
template <> struct Vec<half, 4> {
  using Type = half4;
};
template <> struct Vec<half, 8> {
  using Type = Half8_;
};

template <> struct FloatVec<half> {
  using Type = float;
};
template <> struct FloatVec<half2> {
  using Type = float2;
};
template <> struct FloatVec<half4> {
  using Type = float4;
};
template <> struct FloatVec<Half8_> {
  using Type = Float8_;
};

template <> inline float mul(half a, half b) { return (float)a * (float)b; }
template <> inline half mul(half a, half b) { return a * b; }

template <> inline float2 mul(half2 a, half2 b) {
  return (float2)a * (float2)b;
}
template <> inline half2 mul(half2 a, half2 b) { return a * b; }

template <> inline float4 mul(half4 a, half4 b) {
  return (float4)a * (float4)b;
}
template <> inline half4 mul(half4 a, half4 b) { return a * b; }

template <> inline Float8_ mul(Half8_ a, Half8_ b) {
  float4 x = mul<float4, half4, half4>(a.x, b.x);
  float4 y = mul<float4, half4, half4>(a.y, b.y);
  Float8_ c;
  c.x = x;
  c.y = y;
  return c;
}
template <> inline Half8_ mul(Half8_ a, Half8_ b) {
  Half8_ c;
  c.x = mul<half4, half4, half4>(a.x, b.x);
  c.y = mul<half4, half4, half4>(a.y, b.y);
  return c;
}

template <> inline float sum(half a) { return (float)a; }

template <> inline float sum(half2 a) { return (float)a.x + (float)a.y; }

template <> inline float sum(half4 a) { return a.x + a.y + a.z + a.w; }

template <> inline float sum(Half8_ a) { return sum(a.x) + sum(a.y); }

inline float fma(half a, half b, float c) { return (float)a * (float)b + c; }

inline float2 fma(half2 a, half2 b, float2 c) {
  return (float2)a * (float2)b + c;
}

inline float4 fma(half4 a, half4 b, float4 c) {
  return (float4)a * (float4)b + c;
}

inline Float8_ fma(Half8_ a, Half8_ b, Float8_ c) {
  float4 x = fma(a.x, b.x, c.x);
  float4 y = fma(a.y, b.y, c.y);
  Float8_ res;
  res.x = x;
  res.y = y;
  return res;
}
inline Half8_ fma(Half8_ a, Half8_ b, Half8_ c) {
  Half8_ res;
  res.x = fma(a.x, b.x, c.x);
  res.y = fma(a.y, b.y, c.y);
  return res;
}

inline void from_float(thread half &dst, float src) {
  dst = static_cast<half>(src);
}
inline void from_float(thread half2 &dst, float2 src) {
  dst.x = static_cast<half>(src.x);
  dst.y = static_cast<half>(src.y);
}
inline void from_float(thread half4 &dst, float4 src) {
  dst.x = static_cast<half>(src.x);
  dst.y = static_cast<half>(src.y);
  dst.z = static_cast<half>(src.z);
  dst.w = static_cast<half>(src.w);
}
inline void from_float(thread Half8_ &dst, Float8_ src) {
  half4 x;
  half4 y;
  from_float(x, src.x);
  from_float(y, src.y);
  dst.x = x;
  dst.y = y;
}

// ========================================== FP8 (uchar) vector data types.

// 8‑lane uchar vector – Metal only provides up to uchar4, so build our own.
struct Uchar8_ {
  uchar4 x;
  uchar4 y;
};

// Vec specialisations so Vec<uchar, N>::Type resolves correctly.
template <> struct Vec<uchar, 1> {
  using Type = uchar;
};
template <> struct Vec<uchar, 2> {
  using Type = uchar2;
};
template <> struct Vec<uchar, 4> {
  using Type = uchar4;
};
template <> struct Vec<uchar, 8> {
  using Type = Uchar8_;
};

// General case: not uchar
template <typename T> inline constexpr bool is_uchar() { return false; }

// Specialization: T is uchar
template <> inline constexpr bool is_uchar<uchar>() { return true; }

// Generic fallback – will fail to compile if a required specialisation is
// missing.
template <typename Vec, typename Quant_vec>
inline Vec fp8_convert(const thread Quant_vec &, float scale) {
  static_assert(sizeof(Vec) == 0, "Missing fp8_convert specialisation");
}

// ========================================== FP8 → float/half/bfloat
inline float __dequant_single(uchar v, float scale) {
  return fp8_e4m3_to_float(v) * scale;
}

// ---- 1‑lane ----
template <>
inline float fp8_convert<float, uchar>(const thread uchar &in, float scale) {
  return __dequant_single(in, scale);
}
template <>
inline half fp8_convert<half, uchar>(const thread uchar &in, float scale) {
  return half(__dequant_single(in, scale));
}
template <>
inline bfloat16_t fp8_convert<bfloat16_t, uchar>(const thread uchar &in,
                                                 float scale) {
  return bfloat16_t(__dequant_single(in, scale));
}

// ---- 2‑lane ----
template <>
inline float2 fp8_convert<float2, uchar2>(const thread uchar2 &in,
                                          float scale) {
  return float2(__dequant_single(in.x, scale), __dequant_single(in.y, scale));
}
template <>
inline half2 fp8_convert<half2, uchar2>(const thread uchar2 &in, float scale) {
  half2 out;
  out.x = half(__dequant_single(in.x, scale));
  out.y = half(__dequant_single(in.y, scale));
  return out;
}
template <>
inline Bfloat2_ fp8_convert<Bfloat2_, uchar2>(const thread uchar2 &in,
                                              float scale) {
  Bfloat2_ out;
  out.x = bfloat16_t(__dequant_single(in.x, scale));
  out.y = bfloat16_t(__dequant_single(in.y, scale));
  return out;
}

// ---- 4‑lane ----
template <>
inline float4 fp8_convert<float4, uchar4>(const thread uchar4 &in,
                                          float scale) {
  return float4(__dequant_single(in.x, scale), __dequant_single(in.y, scale),
                __dequant_single(in.z, scale), __dequant_single(in.w, scale));
}
template <>
inline half4 fp8_convert<half4, uchar4>(const thread uchar4 &in, float scale) {
  half4 out;
  out.x = half(__dequant_single(in.x, scale));
  out.y = half(__dequant_single(in.y, scale));
  out.z = half(__dequant_single(in.z, scale));
  out.w = half(__dequant_single(in.w, scale));
  return out;
}
template <>
inline Bfloat4_ fp8_convert<Bfloat4_, uchar4>(const thread uchar4 &in,
                                              float scale) {
  Bfloat4_ out;
  out.x.x = bfloat16_t(__dequant_single(in.x, scale));
  out.x.y = bfloat16_t(__dequant_single(in.y, scale));
  out.y.x = bfloat16_t(__dequant_single(in.z, scale));
  out.y.y = bfloat16_t(__dequant_single(in.w, scale));
  return out;
}

// ---- 8‑lane ----
template <>
inline Float8_ fp8_convert<Float8_, Uchar8_>(const thread Uchar8_ &in,
                                             float scale) {
  Float8_ out;
  out.x =
      float4(__dequant_single(in.x.x, scale), __dequant_single(in.x.y, scale),
             __dequant_single(in.x.z, scale), __dequant_single(in.x.w, scale));
  out.y =
      float4(__dequant_single(in.y.x, scale), __dequant_single(in.y.y, scale),
             __dequant_single(in.y.z, scale), __dequant_single(in.y.w, scale));
  return out;
}
template <>
inline Half8_ fp8_convert<Half8_, Uchar8_>(const thread Uchar8_ &in,
                                           float scale) {
  Half8_ out;
  out.x = half4(half(__dequant_single(in.x.x, scale)),
                half(__dequant_single(in.x.y, scale)),
                half(__dequant_single(in.x.z, scale)),
                half(__dequant_single(in.x.w, scale)));
  out.y = half4(half(__dequant_single(in.y.x, scale)),
                half(__dequant_single(in.y.y, scale)),
                half(__dequant_single(in.y.z, scale)),
                half(__dequant_single(in.y.w, scale)));
  return out;
}
template <>
inline Bfloat8_ fp8_convert<Bfloat8_, Uchar8_>(const thread Uchar8_ &in,
                                               float scale) {
  Bfloat8_ out;
  // first 4
  out.x.x.x = bfloat16_t(__dequant_single(in.x.x, scale));
  out.x.x.y = bfloat16_t(__dequant_single(in.x.y, scale));
  out.x.y.x = bfloat16_t(__dequant_single(in.x.z, scale));
  out.x.y.y = bfloat16_t(__dequant_single(in.x.w, scale));
  // second 4
  out.y.x.x = bfloat16_t(__dequant_single(in.y.x, scale));
  out.y.x.y = bfloat16_t(__dequant_single(in.y.y, scale));
  out.y.y.x = bfloat16_t(__dequant_single(in.y.z, scale));
  out.y.y.y = bfloat16_t(__dequant_single(in.y.w, scale));
  return out;
}

// ========================================== Dot product utilities

// TODO(EricLBuehler): optimize with vectorization
template <int THREAD_GROUP_SIZE, typename Vec, int N>
inline float qk_dot_(const threadgroup Vec (&q)[N], const thread Vec (&k)[N]) {
  // Compute the parallel products for Q*K^T (treat vector lanes separately).
  using A_vec = typename FloatVec<Vec>::Type;
  A_vec qk_vec = mul<A_vec, Vec, Vec>(q[0], k[0]);
#pragma unroll
  for (int ii = 1; ii < N; ++ii) {
    qk_vec = fma(q[ii], k[ii], qk_vec);
  }

  // Finalize the reduction across lanes.
  float qk = sum(qk_vec);
#pragma unroll
  for (int mask = THREAD_GROUP_SIZE / 2; mask >= 1; mask /= 2) {
    qk += simd_shuffle_xor(qk, mask);
  }
  return qk;
}

template <typename T, int THREAD_GROUP_SIZE> struct Qk_dot {
  template <typename Vec, int N>
  static inline float dot(const threadgroup Vec (&q)[N],
                          const thread Vec (&k)[N]) {
    return qk_dot_<THREAD_GROUP_SIZE>(q, k);
  }
};

// ========================================== Block sum utility

// Utility function for attention softmax.
template <int NUM_WARPS, int NUM_SIMD_LANES>
inline float block_sum(threadgroup float *red_smem, float sum, uint simd_tid,
                       uint simd_lid) {
  // Compute the sum per simdgroup.
#pragma unroll
  for (int mask = NUM_SIMD_LANES / 2; mask >= 1; mask /= 2) {
    sum += simd_shuffle_xor(sum, mask);
  }

  // Simd leaders store the data to shared memory.
  if (simd_lid == 0) {
    red_smem[simd_tid] = sum;
  }

  // Make sure the data is in shared memory.
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // The warps compute the final sums.
  if (simd_lid < NUM_WARPS) {
    sum = red_smem[simd_lid];
  }

  // Parallel reduction inside the simd group.
#pragma unroll
  for (int mask = NUM_WARPS / 2; mask >= 1; mask /= 2) {
    sum += simd_shuffle_xor(sum, mask);
  }

  // Broadcast to other threads.
  return simd_shuffle(sum, 0);
}

// ========================================== Paged Attention kernel

#define MAX(a, b) ((a) > (b) ? (a) : (b))
#define MIN(a, b) ((a) < (b) ? (a) : (b))
#define DIVIDE_ROUND_UP(a, b) (((a) + (b) - 1) / (b))

// FORKED: Replaced function_constant with template parameters for MLX compatibility
// Original:
//   constant bool use_partitioning [[function_constant(10)]];
//   constant bool use_alibi [[function_constant(20)]];
//   constant bool use_fp8_scales [[function_constant(30)]];
// Note: use_partitioning is now derived from PARTITION_SIZE > 0
// Note: use_fp8_scales is handled by is_uchar<CACHE_T>() for FP8 cache types

template <typename T, typename CACHE_T, int HEAD_SIZE, int BLOCK_SIZE, int NUM_THREADS,
          int NUM_SIMD_LANES, int PARTITION_SIZE = 0, bool USE_ALIBI = false>
[[kernel]] void paged_attention(
    device float *exp_sums
    [[buffer(0)]], // [num_seqs, num_heads, max_num_partitions] - only used when
                   // use_partitioning
    device float *max_logits
    [[buffer(1)]], // [num_seqs, num_heads, max_num_partitions] - only used when
                   // use_partitioning
    device T *out
    [[buffer(2)]], // [num_seqs, num_heads, max_num_partitions, head_size]
    device const T *q [[buffer(3)]], // [num_seqs, num_heads, head_size]
    device const CACHE_T *k_cache
    [[buffer(4)]], // [num_blocks, num_kv_heads, head_size/x, block_size, x]
    device const CACHE_T *v_cache
    [[buffer(5)]], // [num_blocks, num_kv_heads, head_size, block_size]
    const device float *__restrict__ k_scale
    [[buffer(6)]], // [1] - only used when use_fp8_scales
    const device float *__restrict__ v_scale
    [[buffer(7)]], // [1] - only used when use_fp8_scales
    const constant int &num_kv_heads [[buffer(8)]], // [num_heads]
    const constant float &scale [[buffer(9)]],
    const constant float &softcapping [[buffer(10)]],
    device const uint32_t *block_tables
    [[buffer(11)]], // [num_seqs, max_num_blocks_per_seq]
    device const uint32_t *context_lens [[buffer(12)]], // [num_seqs]
    const constant int &max_num_blocks_per_seq [[buffer(13)]],
    device const float *alibi_slopes
    [[buffer(14)]], // [num_heads] - only used when use_alibi
    const constant int &q_stride [[buffer(15)]],
    const constant int &kv_block_stride [[buffer(16)]],
    const constant int &kv_head_stride [[buffer(17)]],
    // Phase 7: Sliding-window mask for hybrid attention models (Gemma4 et al.).
    // When sliding_window > 0, K positions older than
    // `context_len - sliding_window` are masked out (zero contribution to
    // softmax / V reduction). When sliding_window == 0 the kernel falls
    // back to the original full-context behaviour.
    const constant int &sliding_window [[buffer(18)]],
    threadgroup char *shared_mem [[threadgroup(0)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]],
    uint simd_tid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  const int seq_idx = threadgroup_position_in_grid.y;
  const int partition_idx = threadgroup_position_in_grid.z;
  const int max_num_partitions = threadgroups_per_grid.z;
  const int thread_idx = thread_position_in_threadgroup.x;
  constexpr bool USE_PARTITIONING = PARTITION_SIZE > 0;
  const uint32_t context_len = context_lens[seq_idx];
  if (USE_PARTITIONING && partition_idx * PARTITION_SIZE >= context_len) {
    // No work to do. Terminate the thread block.
    return;
  }

  const int num_context_blocks = DIVIDE_ROUND_UP(context_len, BLOCK_SIZE);
  const int num_blocks_per_partition =
      USE_PARTITIONING ? PARTITION_SIZE / BLOCK_SIZE : num_context_blocks;

  // [start_block_idx, end_block_idx) is the range of blocks to process.
  const int start_block_idx =
      USE_PARTITIONING ? partition_idx * num_blocks_per_partition : 0;
  const int end_block_idx =
      MIN(start_block_idx + num_blocks_per_partition, num_context_blocks);
  const int num_blocks = end_block_idx - start_block_idx;

  // [start_token_idx, end_token_idx) is the range of tokens to process.
  const int start_token_idx = start_block_idx * BLOCK_SIZE;
  const int end_token_idx =
      MIN(start_token_idx + num_blocks * BLOCK_SIZE, context_len);
  const int num_tokens = end_token_idx - start_token_idx;

  constexpr int THREAD_GROUP_SIZE = MAX(NUM_SIMD_LANES / BLOCK_SIZE, 1);
  constexpr int NUM_THREAD_GROUPS =
      NUM_THREADS / THREAD_GROUP_SIZE; // Note: This assumes THREAD_GROUP_SIZE
                                       // divides NUM_THREADS
  assert(NUM_THREADS % THREAD_GROUP_SIZE == 0);
  constexpr int NUM_TOKENS_PER_THREAD_GROUP =
      DIVIDE_ROUND_UP(BLOCK_SIZE, NUM_SIMD_LANES);
  constexpr int NUM_WARPS = NUM_THREADS / NUM_SIMD_LANES;
  const int warp_idx = simd_tid;
  const int lane = simd_lid;

  const int head_idx = threadgroup_position_in_grid.x;
  const int num_heads = threadgroups_per_grid.x;
  const int num_queries_per_kv = num_heads / num_kv_heads;
  const int kv_head_idx = head_idx / num_queries_per_kv;
  // FORKED: Use template parameter instead of function constant
  const float alibi_slope = !USE_ALIBI ? 0.f : alibi_slopes[head_idx];

  // A vector type to store a part of a key or a query.
  // The vector size is configured in such a way that the threads in a thread
  // group fetch or compute 16 bytes at a time. For example, if the size of a
  // thread group is 4 and the data type is half, then the vector size is 16 /
  // (4 * sizeof(half)) == 2.
  constexpr int VEC_SIZE = MAX(16 / (THREAD_GROUP_SIZE * sizeof(T)), 1);
  using K_vec = typename Vec<T, VEC_SIZE>::Type;
  using Q_vec = typename Vec<T, VEC_SIZE>::Type;
  using Quant_vec = typename Vec<CACHE_T, VEC_SIZE>::Type;

  constexpr int NUM_ELEMS_PER_THREAD = HEAD_SIZE / THREAD_GROUP_SIZE;
  constexpr int NUM_VECS_PER_THREAD = NUM_ELEMS_PER_THREAD / VEC_SIZE;

  const int thread_group_idx = thread_idx / THREAD_GROUP_SIZE;
  const int thread_group_offset = thread_idx % THREAD_GROUP_SIZE;

  // Load the query to registers.
  // Each thread in a thread group has a different part of the query.
  // For example, if the thread group size is 4, then the first thread in the
  // group has 0, 4, 8, ... th vectors of the query, and the second thread has
  // 1, 5, 9, ... th vectors of the query, and so on.
  const device T *q_ptr = q + seq_idx * q_stride + head_idx * HEAD_SIZE;
  threadgroup Q_vec q_vecs[THREAD_GROUP_SIZE][NUM_VECS_PER_THREAD];
#pragma unroll
  for (int i = thread_group_idx; i < NUM_VECS_PER_THREAD;
       i += NUM_THREAD_GROUPS) {
    const int vec_idx = thread_group_offset + i * THREAD_GROUP_SIZE;
    q_vecs[thread_group_offset][i] =
        *reinterpret_cast<const device Q_vec *>(q_ptr + vec_idx * VEC_SIZE);
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // Use fp32 on softmax logits for better accuracy
  threadgroup float *logits = reinterpret_cast<threadgroup float *>(shared_mem);
  // Workspace for reduction
  threadgroup float red_smem[2 * NUM_WARPS];

  // x == THREAD_GROUP_SIZE * VEC_SIZE
  // Each thread group fetches x elements from the key at a time.
  constexpr int x = 16 / sizeof(CACHE_T);
  float qk_max = -FLT_MAX;

  // Iterate over the key blocks.
  // Each warp fetches a block of keys for each iteration.
  // Each thread group in a warp fetches a key from the block, and computes
  // dot product with the query.
  const device uint32_t *block_table =
      block_tables + seq_idx * max_num_blocks_per_seq;
  for (int block_idx = start_block_idx + warp_idx; block_idx < end_block_idx;
       block_idx += NUM_WARPS) {
    // NOTE: The block number is stored in int32. However, we cast it to int64
    // because int32 can lead to overflow when this variable is multiplied by
    // large numbers (e.g., kv_block_stride).
    const int64_t physical_block_number =
        static_cast<int64_t>(block_table[block_idx]);

    // Load a key to registers.
    // Each thread in a thread group has a different part of the key.
    // For example, if the thread group size is 4, then the first thread in the
    // group has 0, 4, 8, ... th vectors of the key, and the second thread has
    // 1, 5, 9, ... th vectors of the key, and so on.
    for (int i = 0; i < NUM_TOKENS_PER_THREAD_GROUP; i++) {
      const int physical_block_offset =
          (thread_group_idx + i * NUM_SIMD_LANES) % BLOCK_SIZE;
      const int token_idx = block_idx * BLOCK_SIZE + physical_block_offset;
      K_vec k_vecs[NUM_VECS_PER_THREAD];

#pragma unroll
      for (int j = 0; j < NUM_VECS_PER_THREAD; j++) {
        const device CACHE_T *k_ptr =
            k_cache + physical_block_number * kv_block_stride +
            kv_head_idx * kv_head_stride + physical_block_offset * x;
        const int vec_idx = thread_group_offset + j * THREAD_GROUP_SIZE;
        const int offset1 = (vec_idx * VEC_SIZE) / x;
        const int offset2 = (vec_idx * VEC_SIZE) % x;

        if constexpr (is_uchar<CACHE_T>()) {
          // FP8 support
          Quant_vec k_vec_quant = *reinterpret_cast<const device Quant_vec *>(
              k_ptr + offset1 * BLOCK_SIZE * x + offset2);
          k_vecs[j] = fp8_convert<K_vec, Quant_vec>(k_vec_quant, *k_scale);
        } else {
          // Non-FP8 default
          k_vecs[j] = *reinterpret_cast<const device K_vec *>(
              k_ptr + offset1 * BLOCK_SIZE * x + offset2);
        }
      }

      // Compute dot product.
      // This includes a reduction across the threads in the same thread group.
      float qk = scale * Qk_dot<T, THREAD_GROUP_SIZE>::dot(
                             q_vecs[thread_group_offset], k_vecs);

      // Apply softcapping
      if (softcapping != 1.0) {
        qk = precise::tanh(qk / softcapping) * softcapping;
      }

      // FORKED: Use template parameter instead of function constant
      // Add the ALiBi bias if slopes are given.
      if constexpr (USE_ALIBI) {
        if (alibi_slope != 0) {
          // Compute bias with explicit float precision to minimize precision loss
          int position_offset = token_idx - int(context_len) + 1;
          float alibi_bias = alibi_slope * float(position_offset);
          qk += alibi_bias;
        }
      }

      if (thread_group_offset == 0) {
        // Store the partial reductions to shared memory.
        //
        // Two distinct masks are applied here:
        //
        //   1. Upper-bound mask (`token_idx >= context_len`): kept at
        //      logit=0 + V-zero (see the V loop below) for backward
        //      compatibility with the original kernel. The V-vector for
        //      positions past `context_len` may contain uninitialised
        //      memory / NaN, so zero-V is the safe option.
        //
        //   2. Phase 7 sliding-window mask (`sliding_window > 0` and
        //      `token_idx < context_len - sliding_window`): set
        //      logit=-INFINITY so `exp(-inf - qk_max) == 0` and the
        //      masked contribution drops out of softmax exactly. Unlike
        //      out-of-context positions, sliding-window-evicted slots
        //      hold valid initialised K/V from earlier real tokens — the
        //      cleanest mask is at the softmax step, not via V-zeroing.
        //
        // With `sliding_window == 0` the lower bound collapses to 0 and
        // only the upper-bound mask fires — identical behaviour to the
        // pre-Phase-7 kernel.
        const int sw = sliding_window;
        const uint32_t sliding_lower =
            (sw > 0 && context_len > uint32_t(sw))
                ? (context_len - uint32_t(sw))
                : 0u;
        const bool sliding_evicted =
            sw > 0 && uint32_t(token_idx) < sliding_lower;
        const bool out_of_context = token_idx >= context_len;
        float stored_logit;
        if (out_of_context) {
          stored_logit = 0.f;
        } else if (sliding_evicted) {
          stored_logit = -INFINITY;
        } else {
          stored_logit = qk;
        }
        logits[token_idx - start_token_idx] = stored_logit;
        // Update the max value (exclude masked positions of either kind).
        const bool any_mask = out_of_context || sliding_evicted;
        qk_max = any_mask ? qk_max : max(qk_max, qk);
      }
    }
  }

  // Perform reduction across the threads in the same warp to get the
  // max qk value for each "warp" (not across the thread block yet).
  // The 0-th thread of each thread group already has its max qk value.
#pragma unroll
  for (int mask = NUM_SIMD_LANES / 2; mask >= THREAD_GROUP_SIZE; mask /= 2) {
    qk_max = max(qk_max, simd_shuffle_xor(qk_max, mask));
  }
  if (lane == 0) {
    red_smem[warp_idx] = qk_max;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // Get the max qk value for the sequence.
  qk_max = lane < NUM_WARPS ? red_smem[lane] : -FLT_MAX;
#pragma unroll
  for (int mask = NUM_WARPS / 2; mask >= 1; mask /= 2) {
    qk_max = max(qk_max, simd_shuffle_xor(qk_max, mask));
  }
  // Broadcast the max qk value to all threads.
  qk_max = simd_shuffle(qk_max, 0);

  // Get the sum of the exp values.
  float exp_sum = 0.f;
  for (int i = thread_idx; i < num_tokens; i += NUM_THREADS) {
    float val = exp(logits[i] - qk_max);
    logits[i] = val;
    exp_sum += val;
  }
  exp_sum = block_sum<NUM_WARPS, NUM_SIMD_LANES>(&red_smem[NUM_WARPS], exp_sum,
                                                 simd_tid, simd_lid);

  // Compute softmax.
  const float inv_sum = divide(1.f, exp_sum + 1e-6f);
  for (int i = thread_idx; i < num_tokens; i += NUM_THREADS) {
    logits[i] *= inv_sum;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // FORKED: Removed redundant use_partitioning function constant check
  // USE_PARTITIONING is already derived from PARTITION_SIZE > 0
  // If partitioning is enabled, store the max logit and exp_sum.
  if (USE_PARTITIONING && thread_idx == 0) {
    device float *max_logits_ptr =
        max_logits + seq_idx * num_heads * max_num_partitions +
        head_idx * max_num_partitions + partition_idx;
    *max_logits_ptr = qk_max;
    device float *exp_sums_ptr = exp_sums +
                                 seq_idx * num_heads * max_num_partitions +
                                 head_idx * max_num_partitions + partition_idx;
    *exp_sums_ptr = exp_sum;
  }

  // Each thread will fetch 16 bytes from the value cache at a time.
  constexpr int V_VEC_SIZE = MIN(16 / sizeof(T), BLOCK_SIZE);
  using V_vec = typename Vec<T, V_VEC_SIZE>::Type;
  using L_vec = typename Vec<T, V_VEC_SIZE>::Type;
  using Float_L_vec = typename FloatVec<L_vec>::Type;
  using V_quant_vec = typename Vec<CACHE_T, V_VEC_SIZE>::Type;

  constexpr int NUM_V_VECS_PER_ROW = BLOCK_SIZE / V_VEC_SIZE;
  constexpr int NUM_ROWS_PER_ITER = NUM_SIMD_LANES / NUM_V_VECS_PER_ROW;
  constexpr int NUM_ROWS_PER_THREAD =
      DIVIDE_ROUND_UP(HEAD_SIZE, NUM_ROWS_PER_ITER);

  // NOTE: We use FP32 for the accumulator for better accuracy.
  float accs[NUM_ROWS_PER_THREAD];
#pragma unroll
  for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
    accs[i] = 0.f;
  }

  T zero_value = 0;
  for (int block_idx = start_block_idx + warp_idx; block_idx < end_block_idx;
       block_idx += NUM_WARPS) {
    // NOTE: The block number is stored in int32. However, we cast it to int64
    // because int32 can lead to overflow when this variable is multiplied by
    // large numbers (e.g., kv_block_stride).
    const int64_t physical_block_number =
        static_cast<int64_t>(block_table[block_idx]);
    const int physical_block_offset = (lane % NUM_V_VECS_PER_ROW) * V_VEC_SIZE;
    const int token_idx = block_idx * BLOCK_SIZE + physical_block_offset;
    L_vec logits_vec;
    Float_L_vec logits_float_vec = *reinterpret_cast<threadgroup Float_L_vec *>(
        logits + token_idx - start_token_idx);
    from_float(logits_vec, logits_float_vec);

    const device CACHE_T *v_ptr = v_cache + physical_block_number * kv_block_stride +
                                  kv_head_idx * kv_head_stride;
#pragma unroll
    for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
      const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
      if (row_idx < HEAD_SIZE) {
        const int offset = row_idx * BLOCK_SIZE + physical_block_offset;
        // NOTE: When v_vec contains the tokens that are out of the context,
        // we should explicitly zero out the values since they may contain NaNs.
        // See
        // https://github.com/vllm-project/vllm/issues/641#issuecomment-1682544472
        V_vec v_vec;

        if constexpr (is_uchar<CACHE_T>()) {
          // FP8 support
          V_quant_vec v_quant_vec =
              *reinterpret_cast<const device V_quant_vec *>(v_ptr + offset);
          v_vec = fp8_convert<V_vec, V_quant_vec>(v_quant_vec, *v_scale);
        } else {
          // Non-FP8 default
          v_vec = *reinterpret_cast<const device V_vec *>(v_ptr + offset);
        }

        if (block_idx == num_context_blocks - 1) {
          thread T *v_vec_ptr = reinterpret_cast<thread T *>(&v_vec);
#pragma unroll
          for (int j = 0; j < V_VEC_SIZE; j++) {
            v_vec_ptr[j] =
                token_idx + j < context_len ? v_vec_ptr[j] : zero_value;
          }
        }
        accs[i] += dot(logits_vec, v_vec);
      }
    }
  }

  // Perform reduction within each warp.
#pragma unroll
  for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
    float acc = accs[i];
#pragma unroll
    for (int mask = NUM_V_VECS_PER_ROW / 2; mask >= 1; mask /= 2) {
      acc += simd_shuffle_xor(acc, mask);
    }
    accs[i] = acc;
  }

  // NOTE: A barrier is required because the shared memory space for logits
  // is reused for the output.
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // Perform reduction across warps.
  threadgroup float *out_smem =
      reinterpret_cast<threadgroup float *>(shared_mem);
#pragma unroll
  for (int i = NUM_WARPS; i > 1; i /= 2) {
    int mid = i / 2;
    // Upper warps write to shared memory.
    if (warp_idx >= mid && warp_idx < i) {
      threadgroup float *dst = &out_smem[(warp_idx - mid) * HEAD_SIZE];
#pragma unroll
      for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
        const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
        if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
          dst[row_idx] = accs[i];
        }
      }
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);

    // Lower warps update the output.
    if (warp_idx < mid) {
      const threadgroup float *src = &out_smem[warp_idx * HEAD_SIZE];
#pragma unroll
      for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
        const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
        if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
          accs[i] += src[row_idx];
        }
      }
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);
  }

  // Write the final output.
  if (warp_idx == 0) {
    device T *out_ptr =
        out + seq_idx * num_heads * max_num_partitions * HEAD_SIZE +
        head_idx * max_num_partitions * HEAD_SIZE + partition_idx * HEAD_SIZE;
#pragma unroll
    for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
      const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
      if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
        *(out_ptr + row_idx) = T(accs[i]);
      }
    }
  }
}

// ========================================== Grouped GQA paged attention
//
// Long-context Qwen3.5/3.6 dense and MoE decode use 24Q/4KV or 16Q/2KV at
// head_dim=256. Gemma 4 global attention uses 16Q/1KV at head_dim=512. All use
// block_size=16. The generic kernel above launches one 256-thread threadgroup
// per *query* head. Consequently every query head mapped to one KV head
// traverses the same paged K/V range in independent threadgroups.
//
// This deliberately narrow two-pass specialization mirrors MLX's
// `sdpa_vector_2pass_1/2` geometry: one threadgroup is keyed by a KV head and
// contains one SIMD group per query head (and one set per two-row MTP query).
// The grid z-axis is a large set of strided *logical-block* stripes. Each SIMD
// computes all 16 QK scores in a page with two lanes per token, then every lane
// owns HEAD_SIZE/32 output dimensions and vector-loads that dimension's
// contiguous V[16] row. This preserves the native paged V layout while retaining MLX's
// long-context parallelism, with no threadgroup staging or barriers.
//
// The host dispatcher only selects the explicit BF16 D256 or D512, BS16
// instantiations for their exact head layouts. Keeping this as a two-entry
// template avoids copying the kernel while leaving every other
// model/configuration on the proven fallback.

template <int HEAD_SIZE, bool STAGE_KV>
[[kernel]] void paged_attention_grouped_bfloat16_bs16_striped(
    device float *exp_sums [[buffer(0)]],
    device float *max_logits [[buffer(1)]],
    device bfloat16_t *tmp_out [[buffer(2)]],
    device const bfloat16_t *q [[buffer(3)]],
    device const bfloat16_t *k_cache [[buffer(4)]],
    device const bfloat16_t *v_cache [[buffer(5)]],
    const device float *__restrict__ k_scale [[buffer(6)]],
    const device float *__restrict__ v_scale [[buffer(7)]],
    const constant int &num_kv_heads [[buffer(8)]],
    const constant float &scale [[buffer(9)]],
    const constant float &softcapping [[buffer(10)]],
    device const uint32_t *block_tables [[buffer(11)]],
    device const uint32_t *context_lens [[buffer(12)]],
    const constant int &max_num_blocks_per_seq [[buffer(13)]],
    device const float *alibi_slopes [[buffer(14)]],
    const constant int &q_stride [[buffer(15)]],
    const constant int &kv_block_stride [[buffer(16)]],
    const constant int &kv_head_stride [[buffer(17)]],
    const constant int &sliding_window [[buffer(18)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]],
    uint3 threads_per_threadgroup [[threads_per_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  constexpr int BLOCK_SIZE = 16;
  constexpr int PACKS_PER_HALF_HEAD = HEAD_SIZE / 8 / 2;
  constexpr int OUTPUTS_PER_LANE = HEAD_SIZE / 32;
  constexpr int PAGE_ELEMENTS = HEAD_SIZE * BLOCK_SIZE;

  // Gemma's D512/16Q/1KV group has exactly 512 threads and one physical page
  // is 16 KiB. Cooperatively staging that page lets all 16 query-head SIMD
  // groups reuse one global read. The D256 instantiation sets STAGE_KV=false;
  // its one-element dead tile and all related control flow compile away.
  threadgroup bfloat16_t kv_tile[STAGE_KV ? PAGE_ELEMENTS : 1];

  const int kv_head_idx = int(threadgroup_position_in_grid.x);
  // The exact guard fixes num_seqs=1. Query rows occupy grid.y rather than
  // threadgroup.z so q_len=2 keeps the same per-layout threadgroup occupancy
  // as decode (192 threads for GQA6 or 256 for GQA8); adjacent row groups
  // still traverse identical pages and share GPU caches.
  const int seq_idx = 0;
  const int stripe_idx = int(threadgroup_position_in_grid.z);
  const int num_stripes = int(threadgroups_per_grid.z);
  const int local_q_head = int(thread_position_in_threadgroup.y);
  const int q_pos_in_seq = int(threadgroup_position_in_grid.y);
  const int q_len = int(threadgroups_per_grid.y);
  const int lane = int(thread_position_in_threadgroup.x);
  const int linear_thread =
      int(thread_position_in_threadgroup.y) *
          int(threads_per_threadgroup.x) +
      int(thread_position_in_threadgroup.x);
  const int total_threads =
      int(threads_per_threadgroup.x) * int(threads_per_threadgroup.y);

  // The dispatch guard fixes the supported GQA shape, but derive the query-head
  // index from the actual threadgroup geometry so a host/kernel mismatch fails
  // by producing an obviously invalid launch rather than silently aliasing
  // heads.
  const int gqa_factor = int(threads_per_threadgroup.y);
  const int head_idx = kv_head_idx * gqa_factor + local_q_head;
  const int num_heads = q_stride / HEAD_SIZE;

  const uint32_t context_len = context_lens[seq_idx];
  const int effective_context_len =
      int(context_len) - q_len + q_pos_in_seq + 1;
  const int sliding_lower =
      (sliding_window > 0 && effective_context_len > sliding_window)
          ? (effective_context_len - sliding_window)
          : 0;

  // q_len is one for normal decode and two for the depth-1 MTP verifier.
  // The dispatcher restricts this kernel to a single sequence, so the packed
  // query-row index is exactly q_pos_in_seq.
  const int q_token_idx = q_pos_in_seq;
  const device bfloat16_t *q_ptr =
      q + q_token_idx * q_stride + head_idx * HEAD_SIZE;

  float acc[OUTPUTS_PER_LANE];
#pragma unroll
  for (int i = 0; i < OUTPUTS_PER_LANE; ++i) {
    acc[i] = 0.0f;
  }

  float running_max = -FLT_MAX;
  float running_sum = 0.0f;
  const device uint32_t *block_table =
      block_tables + seq_idx * max_num_blocks_per_seq;

  const int token_in_block = lane / 2;
  const int half_head = lane & 1;
  const int num_context_blocks =
      DIVIDE_ROUND_UP(int(context_len), BLOCK_SIZE);

  // Each SIMD row owns one (query row, query head). A pair of lanes evaluates
  // one token: each lane covers half of the head in BF16x8 packs.
  for (int logical_block = stripe_idx; logical_block < num_context_blocks;
       logical_block += num_stripes) {
    const int block_start_token = logical_block * BLOCK_SIZE;
    if (block_start_token + BLOCK_SIZE <= sliding_lower) {
      continue;
    }
    const int64_t physical_block =
        static_cast<int64_t>(block_table[logical_block]);

    const device bfloat16_t *k_block =
        k_cache + physical_block * int64_t(kv_block_stride) +
        kv_head_idx * kv_head_stride;
    if constexpr (STAGE_KV) {
      for (int element = linear_thread; element < PAGE_ELEMENTS;
           element += total_threads) {
        kv_tile[element] = k_block[element];
      }
      threadgroup_barrier(mem_flags::mem_threadgroup);
    }
    float partial = 0.0f;
#pragma unroll
    for (int pack = 0; pack < PACKS_PER_HALF_HEAD; ++pack) {
      const int head_pack = half_head * PACKS_PER_HALF_HEAD + pack;
      const Bfloat8_ q_pack =
          *reinterpret_cast<const device Bfloat8_ *>(q_ptr + head_pack * 8);
      Bfloat8_ k_pack;
      const int k_offset =
          (head_pack * BLOCK_SIZE + token_in_block) * 8;
      if constexpr (STAGE_KV) {
        k_pack = *reinterpret_cast<const threadgroup Bfloat8_ *>(
            kv_tile + k_offset);
      } else {
        k_pack = *reinterpret_cast<const device Bfloat8_ *>(
            k_block + k_offset);
      }
      partial += sum(mul<Float8_, Bfloat8_, Bfloat8_>(q_pack, k_pack));
    }
    float score = (partial + simd_shuffle_xor(partial, 1)) * scale;
    if (softcapping != 1.0f) {
      score = precise::tanh(score / softcapping) * softcapping;
    }

    const int token_idx = block_start_token + token_in_block;
    const bool valid = token_idx < effective_context_len &&
        token_idx >= sliding_lower;
    const float owned_score = ((lane & 1) == 0 && valid) ? score : -FLT_MAX;
    const float block_max = simd_max(owned_score);
    const float new_max = max(running_max, block_max);
    const float old_factor = running_sum > 0.0f
        ? fast::exp(running_max - new_max)
        : 0.0f;
    const float owned_weight = ((lane & 1) == 0 && valid)
        ? fast::exp(score - new_max)
        : 0.0f;
    const float block_sum = simd_sum(owned_weight);

    float weights[BLOCK_SIZE];
#pragma unroll
    for (int token = 0; token < BLOCK_SIZE; ++token) {
      weights[token] = simd_shuffle(owned_weight, token * 2);
    }

    const bool full_valid_block = block_start_token >= sliding_lower &&
        block_start_token + BLOCK_SIZE <= effective_context_len;
    const device bfloat16_t *v_block =
        v_cache + physical_block * int64_t(kv_block_stride) +
        kv_head_idx * kv_head_stride;
    if constexpr (STAGE_KV) {
      // No thread may overwrite the shared K page until every query-head SIMD
      // has consumed it. Likewise, no V consumer may run before the
      // cooperative replacement is complete.
      threadgroup_barrier(mem_flags::mem_threadgroup);
      for (int element = linear_thread; element < PAGE_ELEMENTS;
           element += total_threads) {
        kv_tile[element] = v_block[element];
      }
      threadgroup_barrier(mem_flags::mem_threadgroup);
    }
    Bfloat8_ weights0_bf16;
    Bfloat8_ weights1_bf16;
    if (full_valid_block) {
      Float8_ weights0;
      weights0.x = float4(weights[0], weights[1], weights[2], weights[3]);
      weights0.y = float4(weights[4], weights[5], weights[6], weights[7]);
      Float8_ weights1;
      weights1.x =
          float4(weights[8], weights[9], weights[10], weights[11]);
      weights1.y =
          float4(weights[12], weights[13], weights[14], weights[15]);
      from_float(weights0_bf16, weights0);
      from_float(weights1_bf16, weights1);
    }
#pragma unroll
    for (int i = 0; i < OUTPUTS_PER_LANE; ++i) {
      const int dim = lane * OUTPUTS_PER_LANE + i;
      const int v_row_offset = dim * BLOCK_SIZE;
      float block_acc = 0.0f;
      if (full_valid_block) {
        Bfloat8_ values0;
        Bfloat8_ values1;
        if constexpr (STAGE_KV) {
          values0 = *reinterpret_cast<const threadgroup Bfloat8_ *>(
              kv_tile + v_row_offset);
          values1 = *reinterpret_cast<const threadgroup Bfloat8_ *>(
              kv_tile + v_row_offset + 8);
        } else {
          values0 = *reinterpret_cast<const device Bfloat8_ *>(
              v_block + v_row_offset);
          values1 = *reinterpret_cast<const device Bfloat8_ *>(
              v_block + v_row_offset + 8);
        }
        block_acc =
            sum(mul<Float8_, Bfloat8_, Bfloat8_>(weights0_bf16, values0)) +
            sum(mul<Float8_, Bfloat8_, Bfloat8_>(weights1_bf16, values1));
      } else {
#pragma unroll
        for (int token = 0; token < BLOCK_SIZE; ++token) {
          const int value_token = block_start_token + token;
          if (value_token < effective_context_len &&
              value_token >= sliding_lower) {
            const bfloat16_t value = STAGE_KV
                ? kv_tile[v_row_offset + token]
                : v_block[v_row_offset + token];
            block_acc += weights[token] * float(value);
          }
        }
      }
      acc[i] = acc[i] * old_factor + block_acc;
    }
    if constexpr (STAGE_KV) {
      // All SIMD groups must finish reading V before the next logical page
      // overwrites the tile with K.
      threadgroup_barrier(mem_flags::mem_threadgroup);
    }
    running_max = new_max;
    running_sum = running_sum * old_factor + block_sum;
  }

  const int row = q_token_idx * num_heads + head_idx;
  const int stats_offset = row * num_stripes + stripe_idx;
  if (simd_lid == 0) {
    exp_sums[stats_offset] = running_sum;
    max_logits[stats_offset] = running_max;
  }

  device bfloat16_t *out_ptr =
      tmp_out + stats_offset * HEAD_SIZE + lane * OUTPUTS_PER_LANE;
#pragma unroll
  for (int i = 0; i < OUTPUTS_PER_LANE; ++i) {
    out_ptr[i] = bfloat16_t(acc[i]);
  }

  // These are intentionally unused for the BF16-only specialization.  Keep
  // the bindings identical to the generic kernel so the host can switch
  // pipelines without rebuilding the argument table.
  (void)k_scale;
  (void)v_scale;
  (void)num_kv_heads;
  (void)alibi_slopes;
}

template <int HEAD_SIZE>
[[kernel]] void paged_attention_grouped_bfloat16_striped_reduce(
    device bfloat16_t *out [[buffer(0)]],
    const device float *exp_sums [[buffer(1)]],
    const device float *max_logits [[buffer(2)]],
    const device bfloat16_t *partials [[buffer(3)]],
    device const uint32_t *context_lens [[buffer(4)]],
    const constant int &num_stripes [[buffer(5)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint simd_gid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  constexpr int NUM_REDUCE_SIMDS = 32;
  constexpr int ELEMS_PER_LANE = HEAD_SIZE / 32;

  const int head_idx = int(threadgroup_position_in_grid.x);
  const int q_pos = int(threadgroup_position_in_grid.y);
  const int num_heads = int(threadgroups_per_grid.x);
  const int row = q_pos * num_heads + head_idx;

  const device float *row_sums = exp_sums + row * num_stripes;
  const device float *row_maxs = max_logits + row * num_stripes;
  const device bfloat16_t *row_partials =
      partials + row * num_stripes * HEAD_SIZE;

  float global_max = -FLT_MAX;
  for (int stripe = int(simd_lid); stripe < num_stripes;
       stripe += NUM_REDUCE_SIMDS) {
    global_max = max(global_max, row_maxs[stripe]);
  }
  global_max = simd_max(global_max);

  float global_sum = 0.0f;
  for (int stripe = int(simd_lid); stripe < num_stripes;
       stripe += NUM_REDUCE_SIMDS) {
    const float factor = fast::exp(row_maxs[stripe] - global_max);
    global_sum += factor * row_sums[stripe];
  }
  global_sum = simd_sum(global_sum);

  float acc[ELEMS_PER_LANE];
#pragma unroll
  for (int i = 0; i < ELEMS_PER_LANE; ++i) {
    acc[i] = 0.0f;
  }
  for (int stripe = int(simd_gid); stripe < num_stripes;
       stripe += NUM_REDUCE_SIMDS) {
    const float factor = fast::exp(row_maxs[stripe] - global_max);
    const device bfloat16_t *partial =
        row_partials + stripe * HEAD_SIZE + int(simd_lid) * ELEMS_PER_LANE;
#pragma unroll
    for (int i = 0; i < ELEMS_PER_LANE; ++i) {
      acc[i] += factor * float(partial[i]);
    }
  }

  // Transpose [contributing SIMD, output lane] in 4 KiB of static
  // threadgroup storage, then reduce all 32 stripe classes per output lane.
  threadgroup float outputs[NUM_REDUCE_SIMDS * 32];
#pragma unroll
  for (int i = 0; i < ELEMS_PER_LANE; ++i) {
    outputs[int(simd_lid) * NUM_REDUCE_SIMDS + int(simd_gid)] = acc[i];
    threadgroup_barrier(mem_flags::mem_threadgroup);
    float value = outputs[int(simd_gid) * 32 + int(simd_lid)];
    value = simd_sum(value);
    value = global_sum == 0.0f ? value : value / global_sum;
    threadgroup_barrier(mem_flags::mem_threadgroup);
    if (simd_lid == 0) {
      out[row * HEAD_SIZE + int(simd_gid) * ELEMS_PER_LANE + i] =
          bfloat16_t(value);
    }
  }

  (void)context_lens;
}

#define instantiate_grouped_bfloat16_attention(head_size, stage_kv)          \
  template [[host_name(                                                       \
      "paged_attention_grouped_bfloat16_hs" #head_size                       \
      "_bs16_striped")]] [[kernel]] void                                     \
  paged_attention_grouped_bfloat16_bs16_striped<head_size, stage_kv>(        \
      device float *exp_sums [[buffer(0)]],                                  \
      device float *max_logits [[buffer(1)]],                                \
      device bfloat16_t *tmp_out [[buffer(2)]],                              \
      device const bfloat16_t *q [[buffer(3)]],                              \
      device const bfloat16_t *k_cache [[buffer(4)]],                        \
      device const bfloat16_t *v_cache [[buffer(5)]],                        \
      const device float *__restrict__ k_scale [[buffer(6)]],                \
      const device float *__restrict__ v_scale [[buffer(7)]],                \
      const constant int &num_kv_heads [[buffer(8)]],                        \
      const constant float &scale [[buffer(9)]],                             \
      const constant float &softcapping [[buffer(10)]],                      \
      device const uint32_t *block_tables [[buffer(11)]],                    \
      device const uint32_t *context_lens [[buffer(12)]],                    \
      const constant int &max_num_blocks_per_seq [[buffer(13)]],             \
      device const float *alibi_slopes [[buffer(14)]],                       \
      const constant int &q_stride [[buffer(15)]],                           \
      const constant int &kv_block_stride [[buffer(16)]],                    \
      const constant int &kv_head_stride [[buffer(17)]],                     \
      const constant int &sliding_window [[buffer(18)]],                     \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],   \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                 \
      uint3 thread_position_in_threadgroup                                   \
          [[thread_position_in_threadgroup]],                                \
      uint3 threads_per_threadgroup [[threads_per_threadgroup]],             \
      uint simd_lid [[thread_index_in_simdgroup]]);                          \
  template [[host_name(                                                       \
      "paged_attention_grouped_bfloat16_hs" #head_size                       \
      "_striped_reduce")]] [[kernel]] void                                   \
  paged_attention_grouped_bfloat16_striped_reduce<head_size>(                \
      device bfloat16_t *out [[buffer(0)]],                                  \
      const device float *exp_sums [[buffer(1)]],                            \
      const device float *max_logits [[buffer(2)]],                          \
      const device bfloat16_t *partials [[buffer(3)]],                       \
      device const uint32_t *context_lens [[buffer(4)]],                     \
      const constant int &num_stripes [[buffer(5)]],                         \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],   \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                 \
      uint simd_gid [[simdgroup_index_in_threadgroup]],                      \
      uint simd_lid [[thread_index_in_simdgroup]]);

instantiate_grouped_bfloat16_attention(256, false);
instantiate_grouped_bfloat16_attention(512, true);

template <typename T, int HEAD_SIZE, int NUM_THREADS, int NUM_SIMD_LANES,
          int PARTITION_SIZE = 0>
[[kernel]] void paged_attention_v2_reduce(
    device T *out [[buffer(0)]], const device float *exp_sums [[buffer(1)]],
    const device float *max_logits [[buffer(2)]],
    const device T *tmp_out [[buffer(3)]],
    device uint32_t *context_lens [[buffer(4)]],
    const constant int &max_num_partitions [[buffer(5)]],
    threadgroup char *shared_mem [[threadgroup(0)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]],
    uint3 threads_per_threadgroup [[threads_per_threadgroup]],
    uint simd_tid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  const int num_heads = threadgroups_per_grid.x;
  const int head_idx = threadgroup_position_in_grid.x;
  const int seq_idx = threadgroup_position_in_grid.y;
  const uint32_t context_len = context_lens[seq_idx];
  const int num_partitions = DIVIDE_ROUND_UP(context_len, PARTITION_SIZE);
  if (num_partitions == 1) {
    // No need to reduce. Only copy tmp_out to out.
    device T *out_ptr =
        out + seq_idx * num_heads * HEAD_SIZE + head_idx * HEAD_SIZE;
    const device T *tmp_out_ptr =
        tmp_out + seq_idx * num_heads * max_num_partitions * HEAD_SIZE +
        head_idx * max_num_partitions * HEAD_SIZE;
    for (int i = thread_position_in_threadgroup.x; i < HEAD_SIZE;
         i += threads_per_threadgroup.x) {
      out_ptr[i] = tmp_out_ptr[i];
    }
    // Terminate the thread block.
    return;
  }

  constexpr int NUM_WARPS = NUM_THREADS / NUM_SIMD_LANES;
  const int warp_idx = simd_tid;
  const int lane = simd_lid;

  // Workspace for reduction.
  threadgroup float red_smem[2 * NUM_WARPS];

  // Load max logits to shared memory.
  threadgroup float *shared_max_logits =
      reinterpret_cast<threadgroup float *>(shared_mem);
  const device float *max_logits_ptr =
      max_logits + seq_idx * num_heads * max_num_partitions +
      head_idx * max_num_partitions;
  float max_logit = -FLT_MAX;
  for (int i = thread_position_in_threadgroup.x; i < num_partitions;
       i += threads_per_threadgroup.x) {
    const float l = max_logits_ptr[i];
    shared_max_logits[i] = l;
    max_logit = max(max_logit, l);
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // Get the global max logit.
  // Reduce within the warp.
#pragma unroll
  for (int mask = NUM_SIMD_LANES / 2; mask >= 1; mask /= 2) {
    max_logit = max(max_logit, simd_shuffle_xor(max_logit, mask));
  }
  if (lane == 0) {
    red_smem[warp_idx] = max_logit;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);
  // Reduce across warps.
  max_logit = lane < NUM_WARPS ? red_smem[lane] : -FLT_MAX;
#pragma unroll
  for (int mask = NUM_WARPS / 2; mask >= 1; mask /= 2) {
    max_logit = max(max_logit, simd_shuffle_xor(max_logit, mask));
  }
  // Broadcast the max value to all threads.
  max_logit = simd_shuffle(max_logit, 0);

  // Load rescaled exp sums to shared memory.
  threadgroup float *shared_exp_sums = reinterpret_cast<threadgroup float *>(
      shared_mem + sizeof(float) * num_partitions);
  const device float *exp_sums_ptr = exp_sums +
                                     seq_idx * num_heads * max_num_partitions +
                                     head_idx * max_num_partitions;
  float global_exp_sum = 0.0f;
  for (int i = thread_position_in_threadgroup.x; i < num_partitions;
       i += threads_per_threadgroup.x) {
    float l = shared_max_logits[i];
    float rescaled_exp_sum = exp_sums_ptr[i] * exp(l - max_logit);
    global_exp_sum += rescaled_exp_sum;
    shared_exp_sums[i] = rescaled_exp_sum;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);
  global_exp_sum = block_sum<NUM_WARPS, NUM_SIMD_LANES>(
      &red_smem[NUM_WARPS], global_exp_sum, simd_tid, simd_lid);
  const float inv_global_exp_sum = divide(1.0f, global_exp_sum + 1e-6f);

  // Aggregate tmp_out to out.
  const device T *tmp_out_ptr =
      tmp_out + seq_idx * num_heads * max_num_partitions * HEAD_SIZE +
      head_idx * max_num_partitions * HEAD_SIZE;
  device T *out_ptr =
      out + seq_idx * num_heads * HEAD_SIZE + head_idx * HEAD_SIZE;
#pragma unroll
  for (int i = thread_position_in_threadgroup.x; i < HEAD_SIZE;
       i += NUM_THREADS) {
    float acc = 0.0f;
    for (int j = 0; j < num_partitions; ++j) {
      acc += float(tmp_out_ptr[j * HEAD_SIZE + i]) * shared_exp_sums[j] *
             inv_global_exp_sum;
    }
    out_ptr[i] = T(acc);
  }
}

// ========================================== Varlen Paged Attention kernel
//
// Phase 4a: multi-row paged attention for speculative decoding (MTP).
//
// The single-row kernel above launches one threadgroup per (head, seq_idx)
// — every sequence contributes exactly one query token, so the global Q
// layout is `[num_seqs, num_heads, head_size]`. With multi-token-prediction
// the verify pass needs D+1 query tokens per sequence (one bonus token + D
// draft tokens), so the existing kernel either has to be called D+1 times
// (sequential round-trips, defeating the speedup) or be generalised to
// accept ragged batches.
//
// This kernel keeps the proven single-row path untouched and provides a
// SIBLING entrypoint that consumes a flat `[total_num_queries, num_heads,
// head_size]` Q tensor plus a `cu_seqlens_q[num_seqs+1]` cumulative count.
// One threadgroup per (head, q_token_idx); each threadgroup binary-searches
// cu_seqlens_q to discover its source sequence so it can fetch the right
// block_table row, context_len, and causal upper bound.
//
// Causal mask formula (matches MTPLX pagedattention.metal:867 exactly):
//     q_len            = cu_seqlens_q[seq_idx+1] - cu_seqlens_q[seq_idx]
//     q_pos_in_seq     = q_token_idx - cu_seqlens_q[seq_idx]
//     effective_ctx    = context_len - q_len + q_pos_in_seq + 1
//
// Hand-traced example: 3 sequences with q_lens=[2,1,4] and
// context_lens=[5,3,6] gives cu_seqlens_q=[0,2,3,7]. q_token_idx=1 (the
// second token of seq 0) sees q_len=2, q_pos_in_seq=1, effective_ctx=5 — it
// attends to all 5 KV positions. q_token_idx=3 (first draft of seq 2) sees
// q_len=4, q_pos_in_seq=0, effective_ctx=3 — it attends only to the
// 3-token prefix that existed before the draft window. This matches the
// causal contract: a query token at logical position P can attend to KV
// positions [0, P+1) and we model P as
// (context_len - q_len + q_pos_in_seq).
//
// Special case: q_lens=[1,1,...,1] with cu_seqlens_q=[0,1,2,...,N] yields
// q_len=1, q_pos_in_seq=0, effective_ctx=context_len for every threadgroup
// — byte-identical to the single-row kernel. The single-seq T=1 parity
// test relies on this property.

// Binary search to find which sequence a global query token belongs to.
//
// In varlen attention, queries from multiple sequences are packed
// contiguously into a flat array:
//   q[0..q_len_0-1] -> seq 0, q[q_len_0..q_len_0+q_len_1-1] -> seq 1, ...
// The kernel launches one threadgroup per (head, query_token) in a flat
// grid. Each threadgroup needs to discover which sequence it belongs to so
// it can look up the correct block_table row, context_len, and causal mask
// boundary.
//
// cu_seqlens_q is sorted ascending: [0, q_len_0, q_len_0+q_len_1, ...].
// Returns seq_idx such that
//     cu_seqlens_q[seq_idx] <= q_token_idx < cu_seqlens_q[seq_idx+1].
//
// O(log num_seqs) — for our typical batch sizes (≤ 64) this is a couple of
// iterations and dwarfs nothing in the kernel's QK/V loops.
inline int find_seq_idx_varlen(const device int32_t *cu_seqlens_q,
                                int q_token_idx, int num_seqs) {
  int lo = 0, hi = num_seqs;
  while (lo < hi) {
    int mid = (lo + hi + 1) / 2;
    if (cu_seqlens_q[mid] <= q_token_idx) {
      lo = mid;
    } else {
      hi = mid - 1;
    }
  }
  return lo;
}

template <typename T, typename CACHE_T, int HEAD_SIZE, int BLOCK_SIZE, int NUM_THREADS,
          int NUM_SIMD_LANES, int PARTITION_SIZE = 0, bool USE_ALIBI = false>
[[kernel]] void paged_attention_varlen(
    device float *exp_sums [[buffer(0)]],
    device float *max_logits [[buffer(1)]],
    device T *out [[buffer(2)]],
    // Q is now ragged: [total_num_queries, num_heads, head_size].
    device const T *q [[buffer(3)]],
    device const CACHE_T *k_cache [[buffer(4)]],
    device const CACHE_T *v_cache [[buffer(5)]],
    const device float *__restrict__ k_scale [[buffer(6)]],
    const device float *__restrict__ v_scale [[buffer(7)]],
    const constant int &num_kv_heads [[buffer(8)]],
    const constant float &scale [[buffer(9)]],
    const constant float &softcapping [[buffer(10)]],
    device const uint32_t *block_tables [[buffer(11)]],
    device const uint32_t *context_lens [[buffer(12)]],
    const constant int &max_num_blocks_per_seq [[buffer(13)]],
    device const float *alibi_slopes [[buffer(14)]],
    const constant int &q_stride [[buffer(15)]],
    const constant int &kv_block_stride [[buffer(16)]],
    const constant int &kv_head_stride [[buffer(17)]],
    const constant int &sliding_window [[buffer(18)]],
    device const int32_t *cu_seqlens_q [[buffer(19)]],  // [num_seqs + 1]
    const constant int &num_seqs [[buffer(20)]],
    threadgroup char *shared_mem [[threadgroup(0)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]],
    uint simd_tid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  // Varlen mapping: y-axis enumerates query tokens (not sequences).
  // Binary-search to translate q_token_idx -> seq_idx so we can look up
  // the right block_table row + context_len + causal bound.
  const int q_token_idx = threadgroup_position_in_grid.y;
  const int seq_idx = find_seq_idx_varlen(cu_seqlens_q, q_token_idx, num_seqs);
  const int q_seq_start = cu_seqlens_q[seq_idx];
  const int q_len = cu_seqlens_q[seq_idx + 1] - q_seq_start;
  const int q_pos_in_seq = q_token_idx - q_seq_start;
  const int partition_idx = threadgroup_position_in_grid.z;
  const int max_num_partitions = threadgroups_per_grid.z;
  const int thread_idx = thread_position_in_threadgroup.x;
  constexpr bool USE_PARTITIONING = PARTITION_SIZE > 0;
  const uint32_t context_len = context_lens[seq_idx];

  // Causal upper bound for this query token. See the header comment above
  // for the derivation. Effective context can shrink below `context_len`
  // (the bonus + draft tokens at the front of the verify window must NOT
  // attend to the most recent KV slots — those slots correspond to
  // future-in-time tokens for them).
  const int effective_context_len =
      (int)context_len - q_len + q_pos_in_seq + 1;
  if (effective_context_len <= 0) {
    // No KV tokens to attend to. The output buffer is zero-initialised by
    // the caller, so leaving accs at zero produces 0 / softmax(0) = 0.
    return;
  }

  if (USE_PARTITIONING && partition_idx * PARTITION_SIZE >= effective_context_len) {
    // This partition lies entirely past the causal cutoff for this query
    // token. The reduce kernel ignores partitions past
    // ceil(effective_context_len / PARTITION_SIZE), so we can safely bail
    // without writing exp_sums / max_logits / tmp_out.
    return;
  }

  const int num_context_blocks = DIVIDE_ROUND_UP(effective_context_len, BLOCK_SIZE);
  const int num_blocks_per_partition =
      USE_PARTITIONING ? PARTITION_SIZE / BLOCK_SIZE : num_context_blocks;

  // [start_block_idx, end_block_idx) is the range of blocks to process.
  const int start_block_idx =
      USE_PARTITIONING ? partition_idx * num_blocks_per_partition : 0;
  const int end_block_idx =
      MIN(start_block_idx + num_blocks_per_partition, num_context_blocks);
  const int num_blocks = end_block_idx - start_block_idx;

  // [start_token_idx, end_token_idx) is the range of tokens to process.
  // Clamp end_token_idx to effective_context_len (not context_len) so the
  // tail block honours the causal cutoff.
  const int start_token_idx = start_block_idx * BLOCK_SIZE;
  const int end_token_idx =
      MIN(start_token_idx + num_blocks * BLOCK_SIZE, effective_context_len);
  const int num_tokens = end_token_idx - start_token_idx;

  constexpr int THREAD_GROUP_SIZE = MAX(NUM_SIMD_LANES / BLOCK_SIZE, 1);
  constexpr int NUM_THREAD_GROUPS =
      NUM_THREADS / THREAD_GROUP_SIZE;
  assert(NUM_THREADS % THREAD_GROUP_SIZE == 0);
  constexpr int NUM_TOKENS_PER_THREAD_GROUP =
      DIVIDE_ROUND_UP(BLOCK_SIZE, NUM_SIMD_LANES);
  constexpr int NUM_WARPS = NUM_THREADS / NUM_SIMD_LANES;
  const int warp_idx = simd_tid;
  const int lane = simd_lid;

  const int head_idx = threadgroup_position_in_grid.x;
  const int num_heads = threadgroups_per_grid.x;
  const int num_queries_per_kv = num_heads / num_kv_heads;
  const int kv_head_idx = head_idx / num_queries_per_kv;
  const float alibi_slope = !USE_ALIBI ? 0.f : alibi_slopes[head_idx];

  constexpr int VEC_SIZE = MAX(16 / (THREAD_GROUP_SIZE * sizeof(T)), 1);
  using K_vec = typename Vec<T, VEC_SIZE>::Type;
  using Q_vec = typename Vec<T, VEC_SIZE>::Type;
  using Quant_vec = typename Vec<CACHE_T, VEC_SIZE>::Type;

  constexpr int NUM_ELEMS_PER_THREAD = HEAD_SIZE / THREAD_GROUP_SIZE;
  constexpr int NUM_VECS_PER_THREAD = NUM_ELEMS_PER_THREAD / VEC_SIZE;

  const int thread_group_idx = thread_idx / THREAD_GROUP_SIZE;
  const int thread_group_offset = thread_idx % THREAD_GROUP_SIZE;

  // Q indexing: ragged layout uses q_token_idx (not seq_idx) as the row.
  const device T *q_ptr = q + q_token_idx * q_stride + head_idx * HEAD_SIZE;
  threadgroup Q_vec q_vecs[THREAD_GROUP_SIZE][NUM_VECS_PER_THREAD];
#pragma unroll
  for (int i = thread_group_idx; i < NUM_VECS_PER_THREAD;
       i += NUM_THREAD_GROUPS) {
    const int vec_idx = thread_group_offset + i * THREAD_GROUP_SIZE;
    q_vecs[thread_group_offset][i] =
        *reinterpret_cast<const device Q_vec *>(q_ptr + vec_idx * VEC_SIZE);
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  threadgroup float *logits = reinterpret_cast<threadgroup float *>(shared_mem);
  threadgroup float red_smem[2 * NUM_WARPS];

  constexpr int x = 16 / sizeof(CACHE_T);
  float qk_max = -FLT_MAX;

  const device uint32_t *block_table =
      block_tables + seq_idx * max_num_blocks_per_seq;
  for (int block_idx = start_block_idx + warp_idx; block_idx < end_block_idx;
       block_idx += NUM_WARPS) {
    const int64_t physical_block_number =
        static_cast<int64_t>(block_table[block_idx]);

    for (int i = 0; i < NUM_TOKENS_PER_THREAD_GROUP; i++) {
      const int physical_block_offset =
          (thread_group_idx + i * NUM_SIMD_LANES) % BLOCK_SIZE;
      const int token_idx = block_idx * BLOCK_SIZE + physical_block_offset;
      K_vec k_vecs[NUM_VECS_PER_THREAD];

#pragma unroll
      for (int j = 0; j < NUM_VECS_PER_THREAD; j++) {
        const device CACHE_T *k_ptr =
            k_cache + physical_block_number * kv_block_stride +
            kv_head_idx * kv_head_stride + physical_block_offset * x;
        const int vec_idx = thread_group_offset + j * THREAD_GROUP_SIZE;
        const int offset1 = (vec_idx * VEC_SIZE) / x;
        const int offset2 = (vec_idx * VEC_SIZE) % x;

        if constexpr (is_uchar<CACHE_T>()) {
          Quant_vec k_vec_quant = *reinterpret_cast<const device Quant_vec *>(
              k_ptr + offset1 * BLOCK_SIZE * x + offset2);
          k_vecs[j] = fp8_convert<K_vec, Quant_vec>(k_vec_quant, *k_scale);
        } else {
          k_vecs[j] = *reinterpret_cast<const device K_vec *>(
              k_ptr + offset1 * BLOCK_SIZE * x + offset2);
        }
      }

      float qk = scale * Qk_dot<T, THREAD_GROUP_SIZE>::dot(
                             q_vecs[thread_group_offset], k_vecs);

      if (softcapping != 1.0) {
        qk = precise::tanh(qk / softcapping) * softcapping;
      }

      if constexpr (USE_ALIBI) {
        if (alibi_slope != 0) {
          // ALiBi bias is referenced to the query's own logical position
          // within the full context (context_len - q_len + q_pos_in_seq),
          // which equals effective_context_len - 1. This matches the
          // single-row kernel for the T=1 case where
          // effective_context_len == context_len.
          int position_offset = token_idx - (effective_context_len - 1);
          float alibi_bias = alibi_slope * float(position_offset);
          qk += alibi_bias;
        }
      }

      if (thread_group_offset == 0) {
        // Two masks, same semantics as the single-row kernel:
        //   1. Causal upper bound: token_idx >= effective_context_len ->
        //      logit=0, V-zero. The single-row kernel masks against
        //      context_len; varlen uses effective_context_len so draft
        //      tokens that haven't been committed yet are excluded.
        //   2. Sliding window (Phase 7): when sliding_window > 0 and
        //      token_idx is older than effective_context_len - sw, mask
        //      with -INFINITY so it drops out of softmax exactly.
        const int sw = sliding_window;
        const int sliding_lower =
            (sw > 0 && effective_context_len > sw)
                ? (effective_context_len - sw)
                : 0;
        const bool sliding_evicted =
            sw > 0 && token_idx < sliding_lower;
        const bool out_of_context = token_idx >= effective_context_len;
        float stored_logit;
        if (out_of_context) {
          stored_logit = 0.f;
        } else if (sliding_evicted) {
          stored_logit = -INFINITY;
        } else {
          stored_logit = qk;
        }
        logits[token_idx - start_token_idx] = stored_logit;
        const bool any_mask = out_of_context || sliding_evicted;
        qk_max = any_mask ? qk_max : max(qk_max, qk);
      }
    }
  }

#pragma unroll
  for (int mask = NUM_SIMD_LANES / 2; mask >= THREAD_GROUP_SIZE; mask /= 2) {
    qk_max = max(qk_max, simd_shuffle_xor(qk_max, mask));
  }
  if (lane == 0) {
    red_smem[warp_idx] = qk_max;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  qk_max = lane < NUM_WARPS ? red_smem[lane] : -FLT_MAX;
#pragma unroll
  for (int mask = NUM_WARPS / 2; mask >= 1; mask /= 2) {
    qk_max = max(qk_max, simd_shuffle_xor(qk_max, mask));
  }
  qk_max = simd_shuffle(qk_max, 0);

  float exp_sum = 0.f;
  for (int i = thread_idx; i < num_tokens; i += NUM_THREADS) {
    float val = exp(logits[i] - qk_max);
    logits[i] = val;
    exp_sum += val;
  }
  exp_sum = block_sum<NUM_WARPS, NUM_SIMD_LANES>(&red_smem[NUM_WARPS], exp_sum,
                                                 simd_tid, simd_lid);

  const float inv_sum = divide(1.f, exp_sum + 1e-6f);
  for (int i = thread_idx; i < num_tokens; i += NUM_THREADS) {
    logits[i] *= inv_sum;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

  // Partition output layout matches the single-row kernel: indexed by
  // (q_token_idx, head_idx, partition_idx) rather than
  // (seq_idx, head_idx, partition_idx). The reduce kernel below mirrors
  // this — it iterates per query token, not per sequence.
  if (USE_PARTITIONING && thread_idx == 0) {
    device float *max_logits_ptr =
        max_logits + q_token_idx * num_heads * max_num_partitions +
        head_idx * max_num_partitions + partition_idx;
    *max_logits_ptr = qk_max;
    device float *exp_sums_ptr = exp_sums +
                                 q_token_idx * num_heads * max_num_partitions +
                                 head_idx * max_num_partitions + partition_idx;
    *exp_sums_ptr = exp_sum;
  }

  constexpr int V_VEC_SIZE = MIN(16 / sizeof(T), BLOCK_SIZE);
  using V_vec = typename Vec<T, V_VEC_SIZE>::Type;
  using L_vec = typename Vec<T, V_VEC_SIZE>::Type;
  using Float_L_vec = typename FloatVec<L_vec>::Type;
  using V_quant_vec = typename Vec<CACHE_T, V_VEC_SIZE>::Type;

  constexpr int NUM_V_VECS_PER_ROW = BLOCK_SIZE / V_VEC_SIZE;
  constexpr int NUM_ROWS_PER_ITER = NUM_SIMD_LANES / NUM_V_VECS_PER_ROW;
  constexpr int NUM_ROWS_PER_THREAD =
      DIVIDE_ROUND_UP(HEAD_SIZE, NUM_ROWS_PER_ITER);

  float accs[NUM_ROWS_PER_THREAD];
#pragma unroll
  for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
    accs[i] = 0.f;
  }

  T zero_value = 0;
  for (int block_idx = start_block_idx + warp_idx; block_idx < end_block_idx;
       block_idx += NUM_WARPS) {
    const int64_t physical_block_number =
        static_cast<int64_t>(block_table[block_idx]);
    const int physical_block_offset = (lane % NUM_V_VECS_PER_ROW) * V_VEC_SIZE;
    const int token_idx = block_idx * BLOCK_SIZE + physical_block_offset;
    L_vec logits_vec;
    Float_L_vec logits_float_vec = *reinterpret_cast<threadgroup Float_L_vec *>(
        logits + token_idx - start_token_idx);
    from_float(logits_vec, logits_float_vec);

    const device CACHE_T *v_ptr = v_cache + physical_block_number * kv_block_stride +
                                  kv_head_idx * kv_head_stride;
#pragma unroll
    for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
      const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
      if (row_idx < HEAD_SIZE) {
        const int offset = row_idx * BLOCK_SIZE + physical_block_offset;
        V_vec v_vec;

        if constexpr (is_uchar<CACHE_T>()) {
          V_quant_vec v_quant_vec =
              *reinterpret_cast<const device V_quant_vec *>(v_ptr + offset);
          v_vec = fp8_convert<V_vec, V_quant_vec>(v_quant_vec, *v_scale);
        } else {
          v_vec = *reinterpret_cast<const device V_vec *>(v_ptr + offset);
        }

        // Zero V lanes past the causal cutoff in the tail block.
        if (block_idx == num_context_blocks - 1) {
          thread T *v_vec_ptr = reinterpret_cast<thread T *>(&v_vec);
#pragma unroll
          for (int j = 0; j < V_VEC_SIZE; j++) {
            v_vec_ptr[j] =
                token_idx + j < effective_context_len ? v_vec_ptr[j] : zero_value;
          }
        }
        accs[i] += dot(logits_vec, v_vec);
      }
    }
  }

#pragma unroll
  for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
    float acc = accs[i];
#pragma unroll
    for (int mask = NUM_V_VECS_PER_ROW / 2; mask >= 1; mask /= 2) {
      acc += simd_shuffle_xor(acc, mask);
    }
    accs[i] = acc;
  }

  threadgroup_barrier(mem_flags::mem_threadgroup);

  threadgroup float *out_smem =
      reinterpret_cast<threadgroup float *>(shared_mem);
#pragma unroll
  for (int i = NUM_WARPS; i > 1; i /= 2) {
    int mid = i / 2;
    if (warp_idx >= mid && warp_idx < i) {
      threadgroup float *dst = &out_smem[(warp_idx - mid) * HEAD_SIZE];
#pragma unroll
      for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
        const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
        if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
          dst[row_idx] = accs[i];
        }
      }
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);

    if (warp_idx < mid) {
      const threadgroup float *src = &out_smem[warp_idx * HEAD_SIZE];
#pragma unroll
      for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
        const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
        if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
          accs[i] += src[row_idx];
        }
      }
    }
    threadgroup_barrier(mem_flags::mem_threadgroup);
  }

  // Output indexed by q_token_idx (not seq_idx).
  if (warp_idx == 0) {
    device T *out_ptr =
        out + q_token_idx * num_heads * max_num_partitions * HEAD_SIZE +
        head_idx * max_num_partitions * HEAD_SIZE + partition_idx * HEAD_SIZE;
#pragma unroll
    for (int i = 0; i < NUM_ROWS_PER_THREAD; i++) {
      const int row_idx = lane / NUM_V_VECS_PER_ROW + i * NUM_ROWS_PER_ITER;
      if (row_idx < HEAD_SIZE && lane % NUM_V_VECS_PER_ROW == 0) {
        *(out_ptr + row_idx) = T(accs[i]);
      }
    }
  }
}

// V2 reduce kernel for varlen. The single-row reduce kernel keys partition
// count off `context_lens[seq_idx]`, but in varlen mode each query token has
// its own effective_context_len bound (a draft token sees less context than
// the bonus token), so we must recompute it here using the same formula as
// the main kernel.
template <typename T, int HEAD_SIZE, int NUM_THREADS, int NUM_SIMD_LANES,
          int PARTITION_SIZE = 0>
[[kernel]] void paged_attention_varlen_v2_reduce(
    device T *out [[buffer(0)]],
    const device float *exp_sums [[buffer(1)]],
    const device float *max_logits [[buffer(2)]],
    const device T *tmp_out [[buffer(3)]],
    device uint32_t *context_lens [[buffer(4)]],
    const constant int &max_num_partitions [[buffer(5)]],
    device const int32_t *cu_seqlens_q [[buffer(6)]],
    const constant int &num_seqs [[buffer(7)]],
    threadgroup char *shared_mem [[threadgroup(0)]],
    uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],
    uint3 threadgroups_per_grid [[threadgroups_per_grid]],
    uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]],
    uint3 threads_per_threadgroup [[threads_per_threadgroup]],
    uint simd_tid [[simdgroup_index_in_threadgroup]],
    uint simd_lid [[thread_index_in_simdgroup]]) {
  const int num_heads = threadgroups_per_grid.x;
  const int head_idx = threadgroup_position_in_grid.x;
  // Varlen: y-axis enumerates query tokens, not sequences.
  const int q_token_idx = threadgroup_position_in_grid.y;
  const int seq_idx = find_seq_idx_varlen(cu_seqlens_q, q_token_idx, num_seqs);
  const int q_seq_start = cu_seqlens_q[seq_idx];
  const int q_len = cu_seqlens_q[seq_idx + 1] - q_seq_start;
  const int q_pos_in_seq = q_token_idx - q_seq_start;
  const uint32_t context_len = context_lens[seq_idx];
  const int effective_context_len =
      (int)context_len - q_len + q_pos_in_seq + 1;
  if (effective_context_len <= 0) {
    // Output is zero-initialised by the caller. No reduction to do.
    return;
  }
  const int num_partitions =
      DIVIDE_ROUND_UP(effective_context_len, PARTITION_SIZE);
  if (num_partitions == 1) {
    device T *out_ptr =
        out + q_token_idx * num_heads * HEAD_SIZE + head_idx * HEAD_SIZE;
    const device T *tmp_out_ptr =
        tmp_out + q_token_idx * num_heads * max_num_partitions * HEAD_SIZE +
        head_idx * max_num_partitions * HEAD_SIZE;
    for (int i = thread_position_in_threadgroup.x; i < HEAD_SIZE;
         i += threads_per_threadgroup.x) {
      out_ptr[i] = tmp_out_ptr[i];
    }
    return;
  }

  constexpr int NUM_WARPS = NUM_THREADS / NUM_SIMD_LANES;
  const int warp_idx = simd_tid;
  const int lane = simd_lid;

  threadgroup float red_smem[2 * NUM_WARPS];

  threadgroup float *shared_max_logits =
      reinterpret_cast<threadgroup float *>(shared_mem);
  const device float *max_logits_ptr =
      max_logits + q_token_idx * num_heads * max_num_partitions +
      head_idx * max_num_partitions;
  float max_logit = -FLT_MAX;
  for (int i = thread_position_in_threadgroup.x; i < num_partitions;
       i += threads_per_threadgroup.x) {
    const float l = max_logits_ptr[i];
    shared_max_logits[i] = l;
    max_logit = max(max_logit, l);
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);

#pragma unroll
  for (int mask = NUM_SIMD_LANES / 2; mask >= 1; mask /= 2) {
    max_logit = max(max_logit, simd_shuffle_xor(max_logit, mask));
  }
  if (lane == 0) {
    red_smem[warp_idx] = max_logit;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);
  max_logit = lane < NUM_WARPS ? red_smem[lane] : -FLT_MAX;
#pragma unroll
  for (int mask = NUM_WARPS / 2; mask >= 1; mask /= 2) {
    max_logit = max(max_logit, simd_shuffle_xor(max_logit, mask));
  }
  max_logit = simd_shuffle(max_logit, 0);

  threadgroup float *shared_exp_sums = reinterpret_cast<threadgroup float *>(
      shared_mem + sizeof(float) * num_partitions);
  const device float *exp_sums_ptr = exp_sums +
                                     q_token_idx * num_heads * max_num_partitions +
                                     head_idx * max_num_partitions;
  float global_exp_sum = 0.0f;
  for (int i = thread_position_in_threadgroup.x; i < num_partitions;
       i += threads_per_threadgroup.x) {
    float l = shared_max_logits[i];
    float rescaled_exp_sum = exp_sums_ptr[i] * exp(l - max_logit);
    global_exp_sum += rescaled_exp_sum;
    shared_exp_sums[i] = rescaled_exp_sum;
  }
  threadgroup_barrier(mem_flags::mem_threadgroup);
  global_exp_sum = block_sum<NUM_WARPS, NUM_SIMD_LANES>(
      &red_smem[NUM_WARPS], global_exp_sum, simd_tid, simd_lid);
  const float inv_global_exp_sum = divide(1.0f, global_exp_sum + 1e-6f);

  const device T *tmp_out_ptr =
      tmp_out + q_token_idx * num_heads * max_num_partitions * HEAD_SIZE +
      head_idx * max_num_partitions * HEAD_SIZE;
  device T *out_ptr =
      out + q_token_idx * num_heads * HEAD_SIZE + head_idx * HEAD_SIZE;
#pragma unroll
  for (int i = thread_position_in_threadgroup.x; i < HEAD_SIZE;
       i += NUM_THREADS) {
    float acc = 0.0f;
    for (int j = 0; j < num_partitions; ++j) {
      acc += float(tmp_out_ptr[j * HEAD_SIZE + i]) * shared_exp_sums[j] *
             inv_global_exp_sum;
    }
    out_ptr[i] = T(acc);
  }
}

// FORKED: Updated macro to include USE_ALIBI template parameter
#define instantiate_paged_attention_impl(type, cache_type, head_size,          \
                                         block_size, num_threads,              \
                                         num_simd_lanes, partition_size,       \
                                         use_alibi, alibi_suffix)              \
  template [[host_name("paged_attention_" #type "_cache_" #cache_type          \
                       "_hs" #head_size "_bs" #block_size "_nt" #num_threads   \
                       "_nsl" #num_simd_lanes                                  \
                       "_ps" #partition_size alibi_suffix)]] [[kernel]] void   \
  paged_attention<type, cache_type, head_size, block_size, num_threads,        \
                  num_simd_lanes, partition_size, use_alibi>(                  \
      device float *exp_sums [[buffer(0)]],                                   \
      device float *max_logits [[buffer(1)]],                                 \
      device type *out [[buffer(2)]], device const type *q [[buffer(3)]],      \
      device const cache_type *k_cache [[buffer(4)]],                          \
      device const cache_type *v_cache [[buffer(5)]],                          \
      const device float *__restrict__ k_scale [[buffer(6)]],                  \
      const device float *__restrict__ v_scale [[buffer(7)]],                  \
      const constant int &num_kv_heads [[buffer(8)]],                          \
      const constant float &scale [[buffer(9)]],                               \
      const constant float &softcapping [[buffer(10)]],                        \
      device const uint32_t *block_tables [[buffer(11)]],                      \
      device const uint32_t *context_lens [[buffer(12)]],                      \
      const constant int &max_num_blocks_per_seq [[buffer(13)]],               \
      device const float *alibi_slopes [[buffer(14)]],                         \
      const constant int &q_stride [[buffer(15)]],                             \
      const constant int &kv_block_stride [[buffer(16)]],                      \
      const constant int &kv_head_stride [[buffer(17)]],                       \
      const constant int &sliding_window [[buffer(18)]],                       \
      threadgroup char *shared_mem [[threadgroup(0)]],                         \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],     \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                   \
      uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]], \
      uint simd_tid [[simdgroup_index_in_threadgroup]],                        \
      uint simd_lid [[thread_index_in_simdgroup]]);

// FORKED: Generate both alibi and non-alibi variants
#define instantiate_paged_attention_inner(type, cache_type, head_size,         \
                                          block_size, num_threads,             \
                                          num_simd_lanes, partition_size)      \
  instantiate_paged_attention_impl(type, cache_type, head_size, block_size,    \
                                   num_threads, num_simd_lanes, partition_size,\
                                   false, "");                                 \
  instantiate_paged_attention_impl(type, cache_type, head_size, block_size,    \
                                   num_threads, num_simd_lanes, partition_size,\
                                   true, "_alibi");

#define instantiate_paged_attention_v2_reduce_inner(                           \
    type, head_size, num_threads, num_simd_lanes, partition_size)              \
  template [[host_name("paged_attention_v2_reduce_" #type "_hs" #head_size     \
                       "_nt" #num_threads "_nsl" #num_simd_lanes               \
                       "_ps" #partition_size)]] [[kernel]] void                \
  paged_attention_v2_reduce<type, head_size, num_threads, num_simd_lanes,      \
                            partition_size>(                                   \
      device type * out [[buffer(0)]],                                         \
      const device float *exp_sums [[buffer(1)]],                              \
      const device float *max_logits [[buffer(2)]],                            \
      const device type *tmp_out [[buffer(3)]],                                \
      device uint32_t *context_lens [[buffer(4)]],                             \
      const constant int &max_num_partitions [[buffer(5)]],                    \
      threadgroup char *shared_mem [[threadgroup(0)]],                         \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],     \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                   \
      uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]], \
      uint3 threads_per_threadgroup [[threads_per_threadgroup]],               \
      uint simd_tid [[simdgroup_index_in_threadgroup]],                        \
      uint simd_lid [[thread_index_in_simdgroup]]);

#define instantiate_paged_attention_heads(                                     \
    type, cache_type, block_size, num_threads, num_simd_lanes, partition_size) \
  instantiate_paged_attention_inner(type, cache_type, 32, block_size,          \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 64, block_size,          \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 80, block_size,          \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 96, block_size,          \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 112, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 120, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 128, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 192, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 256, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);                           \
  instantiate_paged_attention_inner(type, cache_type, 512, block_size,         \
                                    num_threads, num_simd_lanes,               \
                                    partition_size);

#define instantiate_paged_attention_v2_reduce_heads(                           \
    type, num_threads, num_simd_lanes, partition_size)                         \
  instantiate_paged_attention_v2_reduce_inner(type, 32, num_threads,           \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 64, num_threads,           \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 80, num_threads,           \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 96, num_threads,           \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 112, num_threads,          \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 120, num_threads,          \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 128, num_threads,          \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 192, num_threads,          \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 256, num_threads,          \
                                              num_simd_lanes, partition_size); \
  instantiate_paged_attention_v2_reduce_inner(type, 512, num_threads,          \
                                              num_simd_lanes, partition_size);

#define instantiate_paged_attention_block_size(type, cache_type, num_threads,  \
                                               num_simd_lanes, partition_size) \
  instantiate_paged_attention_heads(type, cache_type, 8, num_threads,          \
                                    num_simd_lanes, partition_size);           \
  instantiate_paged_attention_heads(type, cache_type, 16, num_threads,         \
                                    num_simd_lanes, partition_size);           \
  instantiate_paged_attention_heads(type, cache_type, 32, num_threads,         \
                                    num_simd_lanes, partition_size);

// TODO: tune num_threads = 256
// NOTE: partition_size = 0
#define instantiate_paged_attention_v1(type, cache_type, num_simd_lanes)       \
  instantiate_paged_attention_block_size(type, cache_type, 256,                \
                                         num_simd_lanes, 0);

// TODO: tune num_threads = 256
// NOTE: partition_size = 512
#define instantiate_paged_attention_v2(type, cache_type, num_simd_lanes)       \
  instantiate_paged_attention_block_size(type, cache_type, 256,                \
                                         num_simd_lanes, 512);

// TODO: tune num_threads = 256
// NOTE: partition_size = 512
#define instantiate_paged_attention_v2_reduce(type, num_simd_lanes)            \
  instantiate_paged_attention_v2_reduce_heads(type, 256, num_simd_lanes, 512);

instantiate_paged_attention_v1(float, float, 32);
instantiate_paged_attention_v1(bfloat16_t, bfloat16_t, 32);
instantiate_paged_attention_v1(half, half, 32);

instantiate_paged_attention_v1(float, uchar, 32);
instantiate_paged_attention_v1(bfloat16_t, uchar, 32);
instantiate_paged_attention_v1(half, uchar, 32);

instantiate_paged_attention_v2_reduce(float, 32);
instantiate_paged_attention_v2_reduce(bfloat16_t, 32);
instantiate_paged_attention_v2_reduce(half, 32);

instantiate_paged_attention_v2(float, float, 32);
instantiate_paged_attention_v2(bfloat16_t, bfloat16_t, 32);
instantiate_paged_attention_v2(half, half, 32);

instantiate_paged_attention_v2(float, uchar, 32);
instantiate_paged_attention_v2(bfloat16_t, uchar, 32);
instantiate_paged_attention_v2(half, uchar, 32);

// ============================================================================
// Varlen kernel instantiations (Phase 4a).
//
// Same (io_type, cache_type) matrix as the single-row kernels above so the
// Rust dispatcher's MetalDtype routing maps to a varlen kernel for every
// non-varlen kernel it can already pick. Head sizes / block sizes mirror
// the single-row instantiations exactly — see instantiate_paged_attention_*
// macros above for the rationale.
// ============================================================================

#define instantiate_paged_attention_varlen_impl(                               \
    type, cache_type, head_size, block_size, num_threads, num_simd_lanes,      \
    partition_size, use_alibi, alibi_suffix)                                   \
  template [[host_name("paged_attention_varlen_" #type "_cache_" #cache_type   \
                       "_hs" #head_size "_bs" #block_size "_nt" #num_threads   \
                       "_nsl" #num_simd_lanes                                  \
                       "_ps" #partition_size alibi_suffix)]] [[kernel]] void   \
  paged_attention_varlen<type, cache_type, head_size, block_size, num_threads, \
                          num_simd_lanes, partition_size, use_alibi>(          \
      device float *exp_sums [[buffer(0)]],                                    \
      device float *max_logits [[buffer(1)]],                                  \
      device type *out [[buffer(2)]],                                          \
      device const type *q [[buffer(3)]],                                      \
      device const cache_type *k_cache [[buffer(4)]],                          \
      device const cache_type *v_cache [[buffer(5)]],                          \
      const device float *__restrict__ k_scale [[buffer(6)]],                  \
      const device float *__restrict__ v_scale [[buffer(7)]],                  \
      const constant int &num_kv_heads [[buffer(8)]],                          \
      const constant float &scale [[buffer(9)]],                               \
      const constant float &softcapping [[buffer(10)]],                        \
      device const uint32_t *block_tables [[buffer(11)]],                      \
      device const uint32_t *context_lens [[buffer(12)]],                      \
      const constant int &max_num_blocks_per_seq [[buffer(13)]],               \
      device const float *alibi_slopes [[buffer(14)]],                         \
      const constant int &q_stride [[buffer(15)]],                             \
      const constant int &kv_block_stride [[buffer(16)]],                      \
      const constant int &kv_head_stride [[buffer(17)]],                       \
      const constant int &sliding_window [[buffer(18)]],                       \
      device const int32_t *cu_seqlens_q [[buffer(19)]],                       \
      const constant int &num_seqs [[buffer(20)]],                             \
      threadgroup char *shared_mem [[threadgroup(0)]],                         \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],     \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                   \
      uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]], \
      uint simd_tid [[simdgroup_index_in_threadgroup]],                        \
      uint simd_lid [[thread_index_in_simdgroup]]);

#define instantiate_paged_attention_varlen_inner(                              \
    type, cache_type, head_size, block_size, num_threads, num_simd_lanes,      \
    partition_size)                                                            \
  instantiate_paged_attention_varlen_impl(type, cache_type, head_size,         \
                                          block_size, num_threads,             \
                                          num_simd_lanes, partition_size,      \
                                          false, "");                          \
  instantiate_paged_attention_varlen_impl(type, cache_type, head_size,         \
                                          block_size, num_threads,             \
                                          num_simd_lanes, partition_size,      \
                                          true, "_alibi");

#define instantiate_paged_attention_varlen_v2_reduce_inner(                    \
    type, head_size, num_threads, num_simd_lanes, partition_size)              \
  template [[host_name(                                                        \
      "paged_attention_varlen_v2_reduce_" #type "_hs" #head_size               \
      "_nt" #num_threads "_nsl" #num_simd_lanes "_ps" #partition_size)]]       \
  [[kernel]] void paged_attention_varlen_v2_reduce<                            \
      type, head_size, num_threads, num_simd_lanes, partition_size>(           \
      device type * out [[buffer(0)]],                                         \
      const device float *exp_sums [[buffer(1)]],                              \
      const device float *max_logits [[buffer(2)]],                            \
      const device type *tmp_out [[buffer(3)]],                                \
      device uint32_t *context_lens [[buffer(4)]],                             \
      const constant int &max_num_partitions [[buffer(5)]],                    \
      device const int32_t *cu_seqlens_q [[buffer(6)]],                        \
      const constant int &num_seqs [[buffer(7)]],                              \
      threadgroup char *shared_mem [[threadgroup(0)]],                         \
      uint3 threadgroup_position_in_grid [[threadgroup_position_in_grid]],     \
      uint3 threadgroups_per_grid [[threadgroups_per_grid]],                   \
      uint3 thread_position_in_threadgroup [[thread_position_in_threadgroup]], \
      uint3 threads_per_threadgroup [[threads_per_threadgroup]],               \
      uint simd_tid [[simdgroup_index_in_threadgroup]],                        \
      uint simd_lid [[thread_index_in_simdgroup]]);

#define instantiate_paged_attention_varlen_heads(                              \
    type, cache_type, block_size, num_threads, num_simd_lanes, partition_size) \
  instantiate_paged_attention_varlen_inner(type, cache_type, 32, block_size,   \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 64, block_size,   \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 80, block_size,   \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 96, block_size,   \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 112, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 120, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 128, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 192, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 256, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);                   \
  instantiate_paged_attention_varlen_inner(type, cache_type, 512, block_size,  \
                                            num_threads, num_simd_lanes,       \
                                            partition_size);

#define instantiate_paged_attention_varlen_v2_reduce_heads(                    \
    type, num_threads, num_simd_lanes, partition_size)                         \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 32, num_threads, num_simd_lanes, partition_size);                  \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 64, num_threads, num_simd_lanes, partition_size);                  \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 80, num_threads, num_simd_lanes, partition_size);                  \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 96, num_threads, num_simd_lanes, partition_size);                  \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 112, num_threads, num_simd_lanes, partition_size);                 \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 120, num_threads, num_simd_lanes, partition_size);                 \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 128, num_threads, num_simd_lanes, partition_size);                 \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 192, num_threads, num_simd_lanes, partition_size);                 \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 256, num_threads, num_simd_lanes, partition_size);                 \
  instantiate_paged_attention_varlen_v2_reduce_inner(                          \
      type, 512, num_threads, num_simd_lanes, partition_size);

#define instantiate_paged_attention_varlen_block_size(                         \
    type, cache_type, num_threads, num_simd_lanes, partition_size)             \
  instantiate_paged_attention_varlen_heads(type, cache_type, 8, num_threads,   \
                                            num_simd_lanes, partition_size);   \
  instantiate_paged_attention_varlen_heads(type, cache_type, 16, num_threads,  \
                                            num_simd_lanes, partition_size);   \
  instantiate_paged_attention_varlen_heads(type, cache_type, 32, num_threads,  \
                                            num_simd_lanes, partition_size);

#define instantiate_paged_attention_varlen_v1(type, cache_type, num_simd_lanes)\
  instantiate_paged_attention_varlen_block_size(type, cache_type, 256,         \
                                                 num_simd_lanes, 0);

#define instantiate_paged_attention_varlen_v2(type, cache_type, num_simd_lanes)\
  instantiate_paged_attention_varlen_block_size(type, cache_type, 256,         \
                                                 num_simd_lanes, 512);

#define instantiate_paged_attention_varlen_v2_reduce(type, num_simd_lanes)     \
  instantiate_paged_attention_varlen_v2_reduce_heads(type, 256, num_simd_lanes,\
                                                      512);

instantiate_paged_attention_varlen_v1(float, float, 32);
instantiate_paged_attention_varlen_v1(bfloat16_t, bfloat16_t, 32);
instantiate_paged_attention_varlen_v1(half, half, 32);

instantiate_paged_attention_varlen_v1(float, uchar, 32);
instantiate_paged_attention_varlen_v1(bfloat16_t, uchar, 32);
instantiate_paged_attention_varlen_v1(half, uchar, 32);

instantiate_paged_attention_varlen_v2_reduce(float, 32);
instantiate_paged_attention_varlen_v2_reduce(bfloat16_t, 32);
instantiate_paged_attention_varlen_v2_reduce(half, 32);

instantiate_paged_attention_varlen_v2(float, float, 32);
instantiate_paged_attention_varlen_v2(bfloat16_t, bfloat16_t, 32);
instantiate_paged_attention_varlen_v2(half, half, 32);

instantiate_paged_attention_varlen_v2(float, uchar, 32);
instantiate_paged_attention_varlen_v2(bfloat16_t, uchar, 32);
instantiate_paged_attention_varlen_v2(half, uchar, 32);
